#!/bin/tcsh -f 
#SBATCH --job-name baroclinic
#SBATCH -N 25
#SBATCH --account=FY139209
#SBATCH --time=0:50:00

# SNL batch system:
#XPBS -l nodes=60:ppn=4
#XPBS -l nodes=40:ppn=2
#XPBS -A FY081407
#XPBS -W depend=afterany:21212
#XPBS -l walltime=0:30:00
#XPBS -N baroclinic
#XPBS -j oe

# titan -- specify number of nodes
#XPBS -A CLI017DEV 
#XPBS -l nodes=6
#XPBS -l walltime=0:30:00
#XPBS -N baroclinic
#XPBS -j oe

#NCAR batch system: 
#BSUB -x                # exlusive use of node (not_shared)
#BSUB -n 16             # number of tasks
#BSUB -a poe            # select the poe elim
#BSUB -R "span[ptile=16]"       # tasks per node
#BSUB -J baroclinic     # job name
#BSUB -o baro.out         # output filename
#BSUB -e baro.out
#BSUB -W 0:15	        #  wall clock time 
#BSUB -P 39510003
#BSUB -q regular        # queue




# USER specific directores:
set HOMME = ~/codes/homme
set MACH = $HOMME/cmake/machineFiles/redsky.cmake
set wdir = ~/scratch1/preqx


# location of test case and vertical coordinate files 
set input = $HOMME/test/ASP/baroclinic   # test cases
set vcoord = $HOMME/test/vcoord          # location of vertical coordinate files



# steady state parameters:
set qsize = 4             # number of tracers  (0 or 4)
set u_perturb = 1         # size of perturbation (0 or 1)
set rotate = 0            # rotation, in degrees.  0, 45 and 90

# other defaults:
set psurf = 0
set npdg=0
set nu_p = 0
set nu_div = -1
set hsub = 1
set mesh_file = none

# Advection scheme

# ASP tests were all run with Robert filter at 0.005  
# leapfrog for both dynamics and tracers, conservation form
# this used to conserve mass, but now (2010/8) nu_q>0 breaks conservation - 
# probably because prim_state_mod is using a staggered mass formulation
#set smooth = 0.005
#set RK2 = 0    
#set qsplit = 1; set rk_stage_user = 0

#CAM-type:  subcycled, consistent advection, monotone limiter
#              RK 5 stage dynamics
#              RK 3 stage for tracers
set smooth = 0
set RK2 = 1    
set qsplit = 4
set limiter = 8
set rsplit=0
set energy_fixer = -1  # disabled

# tensor HV options
set hypervis_scaling = 0  # 3.2 or 4 to turn on tensor HV



# resolution parameters
set ne = 8    # setup for 8,15,30,60,120?
#set ne = 0


#set ndays = 30
set ndays = 9

if ( $ne == 0 ) then
    # CAM defaults
    set RK2 = 5  # turn on K&G 5 stage optionoption    5 dycore + 3 vis
    set qsplit=1
    set rsplit=3
    set hsub=3

    #set mesh_file = $HOMME/utils/CUBIT_scripts/exodus/arm_30_x8_lowconn.g
    #set tstep=10

    set mesh_file = ~/codes/mapping/grids/poles_30_x4.g
    set tstep=75

    set hypervis_scaling = 3.2  
    set nu = 8.0e-8
    set nu_div = $nu
    #set nu_div = 20e-8 ; set hsub=2
    set nu_q = $nu
    set nu_p = $nu



endif
if ( $ne == 8 ) then
   # 3.75 degree      # bluevista: run takes 4min on 16 processors
   set tstep = 360        # dynamics timestep
   set nu = 2e16
   set nu_q = 2e16
   set resolution = low

endif
if ( $ne == 15 ) then
   # 2 degree   bluevista:  run takes 8min on 64 processors
   set tstep = 180        # dynamics timestep

   set nu = 0.9e16
   set nu_q = .9e16
   set resolution = medium

   # test new consistent advection with nu_p>0:
   set nu_p = $nu_q
   set limiter=4

   # test DG option:
   #set npdg=3 ; set limiter=4 ; set nu_q=0

endif
if ( $ne == 30 ) then
   # 1 degree
   set nu = 9.6e14
   set nu_q = 9.6e14

   set tstep = 90        # dynamics timestep
   set resolution = medium_high


    if ( 0  ) then
        # CAM defaults vert lagangian
        set rsplit = 5 
        set nu = 1e15
        set nu_div = $nu
        #set nu_div = 2.5e15  ; set hsub = 2
        set nu_q = $nu
        set nu_p = $nu

        #set limiter = 4
    endif
    if ( 0  ) then
        # Kinnmark and Gray Options:
        # CAM defaults
        set nu = 1e15
        set nu_q = 1e15
        set nu_div = 2.5e15 
    
        set RK2 = 5  # turn on K&G 5 stage optionoption    5 dycore + 3 vis
        set tstep=360
        set qsplit=1
        set hsub = 3    # 2.5 would work, if possible 
        #set rsplit = 0 ; set nu_p = 0
        set rsplit = 5 ; set nu_p = 1e15
    
        set energy_fixer = -1
    endif
    if ( 0 ) then
       # tensorHV instead of constant coefficient
       set hypervis_scaling = 3.2  
       set nu = 8.0e-8
       set nu_div = $nu
       #set nu_div = 20e-8 ; set hsub=2
       set nu_q = $nu
       set nu_p = $nu
    endif
endif
if ( $ne == 60 ) then
   # 0.5 degree
   set tstep = 40        # dynamics timestep
   set nu = 1e14
   set nu_q = 1e14
   set resolution = high
endif
if ( $ne == 120 ) then
   # 0.25 degree         # blackrose 40 nodes:  17min/day
   set tstep = 20        # tbird 100 nodes:  15days took 3.5h
   set nu = 1.1e13
   set nu_q = 1.1e13
   set resolution = ultra_high
   set ndays = 15
   #set ndays = 1
endif




# number of hours between calls to prim_printstate
set sfreq = 6
@ sfreq *= 3600
set sfreq = `echo "$sfreq / $tstep " | bc`
#@ sfreq /= $tstep

# # construct the run name
# @ rindex = $rotate / 15
# @ case = $u_perturb + 1
# set qindex=0
# if ( $qsize == 4) set qindex=1234

# set caseno = 1
# if ( $u_perturb == 1 ) set caseno = 2
# set name = HOMME-$caseno-$rindex-$qindex-$resolution-L26-nu$nu
# if ( $RK2 == 1 ) then
#    if ( $qsplit == 1 ) then
#       set name = HOMME-$caseno-$rindex-$qindex-$resolution-L26-nu$nu-rk3
#    else
#       set name = HOMME-$caseno-$rindex-$qindex-$resolution-L26-nu$nu-sub$qsplit
#    endif
# endif
# if ( $psurf == 1 ) set name = $name-psurf

set name = jwasp-ne${ne}-nu$nu
if ( $nu != $nu_div ) then
    set name = ${name}-nudiv$nu_div
endif
if ( $RK2 > 1 ) then
    set name = ${name}-RK$RK2-q$qsplit
endif
if ( $rsplit > 0 ) then
    set name = ${name}-r$rsplit
endif
if ( ! ($hypervis_scaling == 0) ) then
    set name = ${name}-tensorHV
endif


echo runname =  $name

# create working directory
mkdir $wdir
mkdir $wdir/bld
mkdir $wdir/$name
mkdir $wdir/$name/movies
set bld = $wdir/bld
set run = $wdir/$name


# create the namelist (input file) based on parameters set above
cd $input
rm -f $run/explicit.nl
sed s/NE/$ne/ explicit26.nl.sed |\
sed s/TSTEP/"$tstep qsplit=$qsplit rsplit=$rsplit psurf_vis=$psurf  "/ |\
sed s/SFREQ/$sfreq/ |\
sed s/tracer_advection_formulation.\*/"tstep_type = $RK2  hypervis_subcycle = $hsub"/ |\
sed s/NU1/$nu/ | sed s/NU2/$nu_q/ | \
sed s:nu_div.\*:"nu_div = $nu_div  npdg=$npdg  mesh_file='$mesh_file'": |\
sed s/nu_p.\*/"nu_p = $nu_p"/ |\
sed s/hypervis_order.\*/"hypervis_order=2  hypervis_scaling=$hypervis_scaling  "/ |\
sed s/u_perturb.\*/"u_perturb = $u_perturb"/ |\
sed s/rotate_grid.\*/"rotate_grid = $rotate"/ |\
sed s/ndays.\*/"ndays = $ndays"/ |\
sed s/energy_fixer.\*/"energy_fixer = $energy_fixer"/ |\
sed s/limiter_option.\*/"limiter_option = $limiter"/ |\
sed s/smooth.\*/"smooth = $smooth"/ |\
sed s/qsize.\*/"qsize = $qsize"/    > $run/input.nl



set NCPU = 12
if ( ${?PBS_NODEFILE} ) then
    set NCPU = `wc $PBS_NODEFILE | awk '{print $1}' - `
endif
if ( ${?PBS_NNODES} ) then
  set NCPU = $PBS_NNODES
endif
if ( ${?LSB_MCPU_HOSTS} ) then
  set NCPU = `wc $LSB_DJOB_HOSTFILE | awk '{print $1}' - `
endif
if ( ${?SLURM_NNODES} ) then
   # SLURM_NNODES  = number of nodes
   # hard to tell how many cores per nodes
   # set NCPU to zero, and mpirun will use the max allowed
   set NCPU = 0
endif
echo using NCPU = $NCPU



set machine = default
if ( yodjag == `hostname | head -c 6` ) set machine = titan
if ( chester == `hostname | head -c 7` ) set machine = titan
if ( titan == `hostname | head -c 5` ) set machine = titan
echo machine = $machine
echo hostname = `hostname`


if ( titan == $machine ) then
   set mpirun = "aprun -n $NCPU "
   # cant reference home directory
   set vcoord = /tmp/work/$USER/preqx/vcoord
   rsync -a $HOMME/test/vcoord/* $vcoord
else
   set mpirun = "mpirun -np $NCPU"
endif

#
#  BUILD
#
cd $bld
set option = run  # command line opption: [build]
if ( $#argv >= 1) set option = $1
if ( ! -f CMakeCache.txt) set option = "build"   # no cmake files.  force build
if ( $option == "build" ) then
   rm -rf CMakeFiles CMakeCache.txt
   cmake -C $MACH -DPREQX_PLEV=26 -DPREQX_NP=4 -DPREQX_USE_ENERGY=TRUE  $HOMME
   make -j4 clean
endif
#make VERBOSE=1 -j4 preqx
make -j4 preqx
if ($status) exit
set exe = $bld/src/preqx/preqx


#
#  RUN
#
cd $run
rm -f vcoord
ln -s $vcoord vcoord

date
$mpirun  $exe x < $run/input.nl
if ($status) exit
date

#ncrename -v .Q,Q1 -v .u,U -v .v,V -v .geos,PHIS -v .geo,PHI  -v .omega,OMEGA \
# -v .ps,PS   movies/asp_baroclinic1.nc  
#ncrename -v .Q,Q1 -v .u,U -v .v,V -v .geos,PHIS -v .geo,PHI  -v .omega,OMEGA \
# -v .ps,PS   movies/asp_baroclinic2.nc  

ncl $input/ps.ncl
ncl $input/Q.ncl
ncl $input/zeta.ncl



