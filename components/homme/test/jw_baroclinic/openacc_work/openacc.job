#!/bin/tcsh -f
#PBS -A STF006
#PBS -l nodes=3
#PBS -q batch
#PBS -l walltime=0:45:00
#PBS -N baroclinic
#PBS -j oe

#  The goal is to configure HOMME to be identical to how it is used in CAM. We propose two configuations:
#  (1) CAM5, with 30 levels and 25 tracers
#  (2) CAM6, with 60 levels and 50 tracers

#  set paths to source code, build directory and run directory
set wdir   = /lustre/atlas1/stf006/scratch/imn/HOMME_ACME             # run directory
set HOMME  = /ccs/home/imn/ACME/components/homme                # HOMME svn checkout     
set input  = $HOMME/test/jw_baroclinic/openacc_work    # input files for test case
set vdir   = $HOMME/test/vcoord            # vertical coordinate files
set exedir = $HOMME/build/preqx
set exe    = preqx.openacc



  source /opt/modules/default/init/tcsh
  setenv MODULEPATH ${MODULEPATH}:/ccs/home/norton/.modules
  module rm PrgEnv-intel
  module rm pgi
  module rm PrgEnv-pgi
  module rm PrgEnv-cray
  module rm PrgEnv-gnu
  module rm PrgEnv-pathscale
  module rm intel
  module rm pgi
  module rm cray
  module rm pathscale
  module rm parallel-netcdf
  module rm netcdf
  module rm cmake
# module rm cray-mpich
  module rm cray-mpich2
  module rm cray-libsci
  module rm xt-libsci
  module rm cray-netcdf
  module rm cray-netcdf-hdf5parallel
  module rm cray-parallel-netcdf
    module load PrgEnv-pgi
    module switch pgi pgi/15.10.0
    module switch cray-mpich cray-mpich/7.2.2
    module switch cray-libsci cray-libsci/13.0.4
    module load esmf/5.2.0rp2
    module switch atp atp/1.7.5
    module add cudatoolkit
    setenv CRAY_CPU_TARGET istanbul
    module load cray-netcdf-hdf5parallel/4.3.3.1
    module load cray-parallel-netcdf/1.5.0
    module load cray-hdf5-parallel
  module load subversion
  module load cmake/2.8.11.2

limit coredumpsize unlimited
limit stacksize unlimited
setenv MPSTKZ 64M
setenv OMP_STACKSIZE 64M
setenv MPICH_ENV_DISPLAY 1
setenv MPICH_VERSION_DISPLAY 1
setenv MPICH_CPUMASK_DISPLAY 1

 setenv CRAY_CUDA_MPS 1
#setenv PMI_NO_FORK 1
#setenv CUDA_PROFILE 1
#setenv CUDA_PROFILE_LOG "cuda_profile_%p.log"
#setenv CUDA_PROFILE_CONFIG ./cuda_config
#setenv PGI_ACC_DEBUG 1

setenv OMP_NUM_THREADS 1
set NCPU = $PBS_NUM_NODES
@ NCPU *= 16
@ NCPU /= $OMP_NUM_THREADS
set NPER = 16
@ NPER /= $OMP_NUM_THREADS
echo using NCPU=$NCPU   NTHREADS=$OMP_NUM_THREADS
set mpirun = "aprun -n $NCPU -N $NPER -d $OMP_NUM_THREADS "
echo $mpirun

set u_perturb = 1
set rotate = 0
set ne    = 8      # horizontal resolution   4,16,30,60,120,240
set nlev  = 64     # vertical resolution  26,30,60,64,96
set qsize = 50     # number of passive tracers
set namelist = jw_baroclinic.nl  # CAM-style, vertically lagrangian
               
#  run each different resolution in a different directory
set name = jw-ne${ne}-nlev${nlev}-qsize${qsize}-thread${OMP_NUM_THREADS}-nodes${PBS_NUM_NODES}
set run = $wdir/$exe/$name

if ( $ne == 4 ) then
   set tstep=1800
   set nu=4.5e17
endif
if ( $ne == 8 ) then
   set tstep=1200
   set nu=5e16
endif
if ( $ne == 16 ) then
   set tstep=600
   set nu=7e15
endif
if ( $ne == 20 ) then
   set tstep=300
   set nu=1e15
endif
if ( $ne == 30 ) then
   set tstep=300
   set nu=1e15
endif
if ( $ne == 60 ) then
   set tstep=150
   set nu=1e14
endif
if ( $ne == 120 ) then
   set tstep=75
   set nu=1e13
endif
if ( $ne == 240 ) then
   set tstep=40
   set nu=1e12
endif

# diagnostics printed to stdout
set sfreq = 8   # number of hours
@ sfreq *= 3600
@ sfreq /= $tstep

mkdir -p $run/movies
cd $run

if ( $nlev == 26 ) then
   set vfile_mid     = "./camm-26.ascii"
   set vfile_int     = "./cami-26.ascii"
   cp $vdir/cam*26.ascii  $run   
else if ( $nlev == 30 ) then
   set vfile_mid     = "./camm-30.ascii"
   set vfile_int     = "./cami-30.ascii"
   cp $vdir/cam*30.ascii  $run   
else if ( $nlev == 60 ) then
   set vfile_mid     = "./aspL60_mid.ascii"
   set vfile_int     = "./aspL60_int.ascii"
   cp $vdir/aspL60_*.ascii $run
else  # default: assume pure sigma levels:
  set vfile_mid     = "./sabm-$nlev.ascii"
  set vfile_int     = "./sabi-$nlev.ascii"
  cp $vdir/sab?-$nlev.ascii  $run   
endif

#  create a new namelist from original ne16 template:
sed s/ne=.\*/ne=$ne/ $input/$namelist |\
sed s/tstep=.\*/tstep=$tstep/ | \
sed s/nu=.\*/nu=$nu/ | \
sed s/nu_div=.\*/nu_div=$nu/ | \
sed s/nu_p=.\*/nu_p=$nu/ | \
sed s/nu_q=.\*/nu_q=$nu/ | \
sed s/NThreads.\*/NThreads=$OMP_NUM_THREADS/ | \
sed s/statefreq.\*/statefreq=$sfreq/ |\
sed s/u_perturb.\*/"u_perturb = $u_perturb"/   |\
sed s/rotate_grid.\*/"rotate_grid = $rotate"/  |\
sed s:vfile_mid.\*:"vfile_mid = '$vfile_mid'":  |\
sed s:vfile_int.\*:"vfile_int = '$vfile_int'":  |\
sed s/qsize.\*/"qsize = $qsize"/  > input.nl

cp $exedir/$exe .
cp ~/HOMME_ACME/cuda_config .
date
$mpirun ./$exe < input.nl
date

