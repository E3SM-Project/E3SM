#!/bin/tcsh -f
#BSUB -P stf006accept
#BSUB -nnodes 1
#BUSB -env "all,JOB_FEATURE=gpumps"
#BSUB -W 15
#BSUB -J baroclinic
#BSUB -o .o%J
#BSUB -e .e%J

#  The goal is to configure HOMME to be identical to how it is used in CAM. We propose two configuations:
#  (1) CAM5, with 30 levels and 25 tracers
#  (2) CAM6, with 60 levels and 50 tracers

#  set paths to source code, build directory and run directory
set wdir   = /gpfs/leto/stf006/proj-shared/imn/HOMME_ACME             # run directory
set HOMME  = /ccs/home/imn/ACME/components/homme                # HOMME svn checkout     
set input  = $HOMME/test/jw_baroclinic/openacc_work    # input files for test case
set vdir   = $HOMME/test/vcoord            # vertical coordinate files
set exedir = $HOMME/build/preqx
set exe    = preqx.cpu

source $MODULESHOME/init/tcsh
module purge
module load DefApps
module load pgi/17.10
module load netcdf
module load netcdf-fortran
module load parallel-netcdf
module load cmake
module load cuda

limit coredumpsize unlimited
limit stacksize unlimited
setenv MPSTKZ 64M
setenv OMP_STACKSIZE 64M


#I'm going to set one resource set per GPU
#For standalone HOMME on summitdev, the fastest configuration is 2-way threading with 40 MPI tasks per node
set NUM_NODES=1
set TASKS_PER_RS=10
set RS_PER_HOST=4
setenv OMP_NUM_THREADS 2
set NCPU = $NUM_NODES
@ NCPU *= $RS_PER_HOST
@ NCPU *= $TASKS_PER_RS
set NRS = $NUM_NODES
@ NRS *= $RS_PER_HOST
set mpirun = "jsrun --nrs $NRS --tasks_per_rs $TASKS_PER_RS --cpu_per_rs 5 --gpu_per_rs 1 --latency_priority gpu-cpu"
echo $mpirun

set u_perturb = 1
set rotate = 0
set ne    =  8     # horizontal resolution   4,16,30,60,120,240
set nlev  = 72     # vertical resolution  26,30,60,64,96
set qsize = 40     # number of passive tracers
set namelist = jw_baroclinic.nl  # CAM-style, vertically lagrangian
               
#  run each different resolution in a different directory
set name = jw-ne${ne}-nlev${nlev}-qsize${qsize}-thread${OMP_NUM_THREADS}-nodes${NUM_NODES}-tasks${NCPU}
set run = $wdir/$exe/$name

if ( $ne == 4 ) then
   set tstep=1800
   set nu=4.5e17
endif
if ( $ne == 8 ) then
   set tstep=1200
   set nu=5e16
endif
if ( $ne == 16 ) then
   set tstep=600
   set nu=7e15
endif
if ( $ne == 20 ) then
   set tstep=300
   set nu=1e15
endif
if ( $ne == 30 ) then
   set tstep=300
   set nu=1e15
endif
if ( $ne == 60 ) then
   set tstep=150
   set nu=1e14
endif
if ( $ne == 120 ) then
   set tstep=75
   set nu=1e13
endif
if ( $ne == 240 ) then
   set tstep=40
   set nu=1e12
endif

# diagnostics printed to stdout
set sfreq = 8   # number of hours
@ sfreq *= 3600
@ sfreq /= $tstep

mkdir -p $run/movies
cd $run

if ( $nlev == 26 ) then
   set vfile_mid     = "./camm-26.ascii"
   set vfile_int     = "./cami-26.ascii"
   cp $vdir/cam*26.ascii  $run   
else if ( $nlev == 30 ) then
   set vfile_mid     = "./camm-30.ascii"
   set vfile_int     = "./cami-30.ascii"
   cp $vdir/cam*30.ascii  $run   
else if ( $nlev == 60 ) then
   set vfile_mid     = "./aspL60_mid.ascii"
   set vfile_int     = "./aspL60_int.ascii"
   cp $vdir/aspL60_*.ascii $run
else  # default: assume pure sigma levels:
  set vfile_mid     = "./sabm-$nlev.ascii"
  set vfile_int     = "./sabi-$nlev.ascii"
  cp $vdir/sab?-$nlev.ascii  $run   
endif

#  create a new namelist from original ne16 template:
sed s/ne=.\*/ne=$ne/ $input/$namelist |\
sed s/tstep=.\*/tstep=$tstep/ | \
sed s/nu=.\*/nu=$nu/ | \
sed s/nu_div=.\*/nu_div=$nu/ | \
sed s/nu_p=.\*/nu_p=$nu/ | \
sed s/nu_q=.\*/nu_q=$nu/ | \
sed s/NThreads.\*/NThreads=$OMP_NUM_THREADS/ | \
sed s/statefreq.\*/statefreq=$sfreq/ |\
sed s/u_perturb.\*/"u_perturb = $u_perturb"/   |\
sed s/rotate_grid.\*/"rotate_grid = $rotate"/  |\
sed s:vfile_mid.\*:"vfile_mid = '$vfile_mid'":  |\
sed s:vfile_int.\*:"vfile_int = '$vfile_int'":  |\
sed s/qsize.\*/"qsize = $qsize"/  > input.nl

cp $exedir/$exe .

date
$mpirun ./$exe < input.nl
date

