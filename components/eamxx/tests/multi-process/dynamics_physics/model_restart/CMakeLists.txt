include (ScreamUtils)

# This test requires CPRNC
include (BuildCprnc)
BuildCprnc()

# Get or create the dynamics lib
#                 HOMME_TARGET   NP PLEV QSIZE_D
CreateDynamicsLib("theta-l_kokkos"  4   72   10)

# We have 2 runs:
#  1) run for 2*N time steps starting from t=0 (base run), write restart files at Nth step
#  3) run for N time steps re-starting from t=N*dt (rest run)
# We can use the same namelist for all tests. For input/output yaml files, we use the same
# yaml template, but need to configure them b/c the run_t0 and the output file prefix differ
# in the 2 runs.

# Create a single executable for all the 3 runs
CreateADUnitTestExec(model_restart
  LIBS cld_fraction ${dynLibName} shoc p3 scream_rrtmgp)

# Set time integration options
set (CASE_T0 2023-01-01-00000)
set (RUN_DT 30)

set (HIST_FREQ 90)
set (HIST_FREQ_UNITS nsecs)
set (REST_FREQ 60)
set (REST_FREQ_UNITS nsecs)

# Create the baseline (run all timsteps in a single run) as well as the restart files
set(SUFFIX "base")
set (RUN_T0 2023-01-01-00000)
set (RUN_NSTEPS 3)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/input.yaml
               ${CMAKE_CURRENT_BINARY_DIR}/input_base.yaml)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/output.yaml
               ${CMAKE_CURRENT_BINARY_DIR}/output_base.yaml)
CreateUnitTestFromExec(model_restart_base model_restart
                        EXE_ARGS "--args -ifile=input_base.yaml"
                        MPI_RANKS ${SCREAM_TEST_MAX_RANKS}
                        FIXTURES_SETUP base_run
                        PROPERTIES RESOURCE_LOCK rpointer_file)

# Restart the simulation, and run the second half of the time steps
set(SUFFIX "rest")
set (RUN_T0 2023-01-01-00060)
set (RUN_NSTEPS 1)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/input.yaml
               ${CMAKE_CURRENT_BINARY_DIR}/input_rest.yaml)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/output.yaml
               ${CMAKE_CURRENT_BINARY_DIR}/output_rest.yaml)
CreateUnitTestFromExec(model_restart_rest model_restart
                        EXE_ARGS "--args -ifile=input_rest.yaml"
                        MPI_RANKS ${SCREAM_TEST_MAX_RANKS}
                        FIXTURES_REQUIRED base_run
                        FIXTURES_SETUP rest_run
                        PROPERTIES RESOURCE_LOCK rpointer_file)

# Finally, compare the nc outputs generated by the base and rest runs
# IMPORTANT: make sure these file names match what base/rest runs produced
set (SRC_FILE model_output_base.AVERAGE.${HIST_FREQ_UNITS}_x${HIST_FREQ}.np${SCREAM_TEST_MAX_RANKS}.${CASE_T0}.nc)
set (TGT_FILE model_output_rest.AVERAGE.${HIST_FREQ_UNITS}_x${HIST_FREQ}.np${SCREAM_TEST_MAX_RANKS}.${CASE_T0}.nc)

add_test (NAME model_restart_check_np${SCREAM_TEST_MAX_RANKS}
          COMMAND cmake -P ${CMAKE_BINARY_DIR}/bin/CprncTest.cmake ${SRC_FILE} ${TGT_FILE}
          WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
set_tests_properties (model_restart_check_np${SCREAM_TEST_MAX_RANKS} PROPERTIES
                      RESOURCE_GROUPS "devices:1"
                      FIXTURES_REQUIRED "base_run;rest_run")

# Set homme's test options, so that we can configure the namelist correctly
# Discretization/algorithm settings
set (HOMME_TEST_NE 2)
set (HOMME_TEST_LIM 9)
set (HOMME_TEST_REMAP_FACTOR 1)
set (HOMME_TEST_TRACERS_FACTOR 1)
set (HOMME_TEST_TIME_STEP 30)
set (HOMME_THETA_FORM 1)
set (HOMME_TTYPE 10)
set (HOMME_SE_FTYPE 0)
set (HOMME_TEST_TRANSPORT_ALG 0)
set (HOMME_TEST_CUBED_SPHERE_MAP 0)

# Hyperviscosity settings
set (HOMME_TEST_HVSCALING 0)
set (HOMME_TEST_HVS 1)
set (HOMME_TEST_HVS_TOM 0)
set (HOMME_TEST_HVS_Q 1)

set (HOMME_TEST_NU 7e15)
set (HOMME_TEST_NUDIV 1e15)
set (HOMME_TEST_NUTOP 2.5e5)

# Testcase settings
set (HOMME_TEST_MOISTURE notdry)
set (HOMME_THETA_HY_MODE .false.)

# Vert coord settings
set (HOMME_TEST_VCOORD_INT_FILE acme-72i.ascii)
set (HOMME_TEST_VCOORD_MID_FILE acme-72m.ascii)

# Configure the namelist into the test directory
configure_file(${SCREAM_SRC_DIR}/dynamics/homme/tests/theta.nl
               ${CMAKE_CURRENT_BINARY_DIR}/namelist.nl)

# Ensure test input files are present in the data dir
GetInputFile(scream/init/${EAMxx_tests_IC_FILE_72lev})
GetInputFile(cam/topo/${EAMxx_tests_TOPO_FILE})
