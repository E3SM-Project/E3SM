# Core types and configuration options
add_subdirectory(core)

# Low-level scorpio interfaces
add_subdirectory(scorpio_interface)

# Small/generic utilities
add_subdirectory(util)

# Field structures
add_subdirectory(field)

# Grid structures
add_subdirectory(grid)

# Property checks structures
add_subdirectory(property_checks)

# Remapper structures
add_subdirectory(remap)

# Data managers structures
add_subdirectory(data_managers)

set(SHARE_SRC
  atm_process/atmosphere_process.cpp
  atm_process/atmosphere_process_hash.cpp
  atm_process/atmosphere_process_group.cpp
  atm_process/atmosphere_process_dag.cpp
  atm_process/atmosphere_diagnostic.cpp
  atm_process/IOPDataManager.cpp
  algorithm/eamxx_data_interpolation.cpp
  algorithm/eamxx_fv_phys_rrtmgp_active_gases_workaround.cpp
  algorithm/eamxx_time_interpolation.cpp
)

add_library(scream_share ${SHARE_SRC})

target_include_directories(scream_share PUBLIC
  ${SCREAM_SRC_DIR}
  ${SCREAM_BIN_DIR}/src
  ${CMAKE_CURRENT_SOURCE_DIR}
)

target_link_libraries(scream_share PUBLIC
  eamxx_core
  eamxx_scorpio_interface
  eamxx_utils
  eamxx_field
  eamxx_grid
  eamxx_remap
  eamxx_property_checks
  eamxx_data_managers)

# Used in the data interpolation
target_link_libraries(scream_share PUBLIC stdc++fs)

target_link_libraries(scream_share PUBLIC ekat::AllLibs pioc)

# The "build_cf_dictionary" target downloads an XML file containing valid field
# names and aliases based on the CF conventions, and transforms it into a YAML
# file that we use to validate our own fields against it. This target is only
# available if CMake finds wget on your system.
find_program(WGET wget)
if (NOT WGET STREQUAL "WGET-NOTFOUND")
  set(CF_CONVENTIONS_XML_FILE cf-standard-name-table.xml)
  set(CF_CONVENTIONS_URL "http://cfconventions.org/Data/cf-standard-names/76/src/${CF_CONVENTIONS_XML_FILE}")
  add_custom_target(build_cf_dictionary
    ${WGET} ${CF_CONVENTIONS_URL}
    COMMAND ${PROJECT_SOURCE_DIR}/scripts/cf-xml-to-yaml ${CF_CONVENTIONS_XML_FILE}
    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/data
    COMMENT "Downloading CF conventions XML file and building YAML dictionary...")
endif()

# IO library
add_subdirectory(io)

if (NOT SCREAM_LIB_ONLY)
  # Create test_support lib
  add_library(scream_test_support
    data_managers/mesh_free_grids_manager.cpp
  )
  target_link_libraries(scream_test_support PUBLIC scream_share scream_io)
  target_include_directories (scream_test_support PUBLIC ${EKAT_SOURCE_DIR}/extern/Catch2/single_include)

  add_subdirectory(tests)
  add_subdirectory(io/tests)
endif()
