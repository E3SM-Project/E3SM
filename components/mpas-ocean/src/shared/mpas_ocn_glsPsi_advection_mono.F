! Copyright (c) 2013,  Los Alamos National Security, LLC (LANS)
! and the University Corporation for Atmospheric Research (UCAR).
!
! Unless noted otherwise source code is licensed under the BSD license.
! Additional copyright and license information can be found in the LICENSE file
! distributed with this code, or at http://mpas-dev.github.com/license.html
!
!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
!
!  ocn_glsPsi_advection_mono
!
!> \brief MPAS monotonic glsPsi advection with FCT
!> \author Luke Van Roekel
!> \date   updated June 2021
!> \details
!>  This module contains routines for monotonic advection of glsPsi (Umlauf and Burchard 2003)
!>  using a Flux Corrected Transport (FCT) algorithm, which is based on 
!>  the tracer advection mono routines.
!
!-------------------------------------------------------------------------------

module ocn_glsPsi_advection_mono

   ! module includes
#ifdef _ADV_TIMERS
   use mpas_timer
#endif
   use mpas_kind_types
   use mpas_derived_types
   use mpas_dmpar
   use mpas_pool_routines
   use mpas_io_units
   use mpas_threading
   use mpas_tracer_advection_helpers

   use ocn_mesh
   use ocn_constants

   implicit none
   private
   save

   ! module private variables
   real (kind=RKIND) ::  &
      coef3rdOrder        !< high-order horizontal coefficient
   logical ::            &
      monotonicityCheck   !< flag to check monotonicity
   integer ::            &
      vertOrder           !< choice of order for vertical advection
   integer, parameter :: &! enum for supported vertical algorithm order
      vertOrder2 = 2,    &!< 2nd order scheme
      vertOrder3 = 3,    &!< 3rd order scheme
      vertOrder4 = 4      !< 4th order scheme

   ! These temporary allocatable arrays will eventually be moved back into
   ! the tendency routine, but must be declared here for now so that they
   ! retain the shared attribute. Once the threading model has been changed,
   ! these should become thread private again with reduced size

   real (kind=RKIND), dimension(:,:), allocatable :: &
      glsPsiMax,     &! max glsPsi in neighbors for limiting
      glsPsiMin,     &! min glsPsi in neighbors for limiting
      hNewInv,       &! inverse of new layer thickness
      hProv,         &! provisional layer thickness
      hProvInv,      &! inverse of provisional layer thickness
      flxIn,         &! flux coming into each cell
      flxOut,        &! flux going out of each cell
      workTend,      &! temp for holding some tendency values
      lowOrderFlx,   &! low order flux for FCT
      highOrderFlx    ! high order flux for FCT

   ! public method interfaces
   public :: ocn_glsPsi_advection_mono_tend, &
             ocn_glsPsi_advection_mono_init

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

   contains

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
!
!  routine ocn_glsPsi_advection_mono_tend
!
!> \brief MPAS monotonic glsPsi horizontal advection tendency with FCT
!> \author Luke Van Roekel 
!> \date   updated June 2021
!> \details
!>  This routine computes the monotonic glsPsi horizontal advection tendency
!>  using a flux-corrected transport (FCT) algorithm.  This is based on the 
!>  tracer advection mono routines.
!
!-------------------------------------------------------------------------------

   subroutine ocn_glsPsi_advection_mono_tend(tend, glsPsi, layerThickness,    &
                                             normalThicknessFlux, w, dt,       &
                                             advCoefs, advCoefs3rd,            &
                                             nAdvCellsForEdge, advCellsForEdge,&
                                             highOrderAdvectionMask)

      !*** Input/Output parameters

      real (kind=RKIND), dimension(:,:), intent(inout) :: &
         tend            !< [in,out] glsPsi tendency to which advection added

      !*** Input parameters

      real (kind=RKIND), dimension(:,:), intent(in) :: &
         glsPsi                 !< [in] Current glsPsi values
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         layerThickness      !< [in] Thickness
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         normalThicknessFlux !< [in] Thichness weighted velocitiy
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         w                   !< [in] Vertical velocity
      real (kind=RKIND), intent(in) :: &
         dt                  !< [in] Timestep
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         advCoefs            !< [in] Coefficients for 2nd order advection
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         advCoefs3rd         !< [in] Coeffs for missing 3rd/4th order advection
      integer, dimension(:), intent(in) :: &
         nAdvCellsForEdge    !< [in] Number of advection cells for each edge
      integer, dimension(:,:), intent(in) :: &
         advCellsForEdge     !< [in] List of advection cells for each edge
      real (kind=RKIND), dimension(:,:), intent(in) :: &
         highOrderAdvectionMask !< [in] Mask for high order advection

      !*** Local variables

      integer ::          &
         i, iCell, iEdge, &! horz indices
         cell1, cell2,    &! neighbor cell indices
         nCells, nEdges,  &! numbers of cells or edges
         k,k2,kmax         ! vert index variants

      real (kind=RKIND) ::  &
         signedFactor,      &! temp factor including flux sign
         glsPsiNew,         &! updated glsPsi
         glsPsiMinNew,      &! updated glsPsi minimum
         glsPsiMaxNew,      &! updated glsPsi maximum
         glsPsiUpwindNew,   &! glsPsi updated with upwind flx
         scaleFactor,       &! factor for normalizing fluxes
         flux,              &! flux temporary
         glsPsiWeight,      &! glsPsi weighting temporary
         invAreaCell1,      &! inverse cell area
         invAreaCell2,      &! inverse cell area
         verticalWeightK,   &! vertical weighting
         verticalWeightKm1, &! vertical weighting
         coef1, coef3        ! temporary coefficients

      real (kind=RKIND), dimension(nVertLevels) :: &
         wgtTmp,            &! vertical temporaries
         flxTmp, sgnTmp      !    for high-order flux computation

      real (kind=RKIND), parameter :: &
         eps = 1.e-10_RKIND  ! small number to avoid numerical difficulties

      ! end of preamble
      !----------------
      ! begin code

#ifdef _ADV_TIMERS
      call mpas_timer_start('startup')
#endif
      ! Get dimensions
      nCells = nCellsAll
      nEdges = nEdgesAll

      ! allocate temporary arrays
      allocate(glsPsiMin   (nVertLevels  ,nCells), &
               glsPsiMax   (nVertLevels  ,nCells), &
               hNewInv     (nVertLevels  ,nCells), &
               hProv       (nVertLevels  ,nCells), &
               hProvInv    (nVertLevels  ,nCells), &
               flxIn       (nVertLevels  ,nCells+1), &
               flxOut      (nVertLevels  ,nCells+1), &
               workTend    (nVertLevels  ,nCells+1), &
               lowOrderFlx (nVertLevels+1,max(nCells,nEdges)+1), &
               highOrderFlx(nVertLevels+1,max(nCells,nEdges)+1))

      ! Compute some provisional layer thicknesses
      ! Note: This assumes we are in the first part of the horizontal/
      ! vertical operator splitting, which is true because currently
      ! we dont flip order and horizontal is always first.
      ! See notes in commit 2cd4a89d.

      !$omp parallel
      !$omp do schedule(runtime) private(invAreaCell1, kmax, k, i, iEdge, signedFactor)
      do iCell = 1, nCells
        invAreaCell1 = dt/areaCell(iCell)
        kmax = maxLevelCell(iCell)
        do k = 1, kmax
          hProv(k, iCell) = layerThickness(k, iCell)
        end do
        do i = 1, nEdgesOnCell(iCell)
          iEdge = edgesOnCell(i,iCell)
          signedFactor = invAreaCell1*dvEdge(iEdge)*edgeSignOnCell(i,iCell)
          ! Provisional layer thickness is after horizontal thickness flux only
          do k = 1, kmax
            hProv(k, iCell) = hProv(k, iCell) &
                            + signedFactor*normalThicknessFlux(k,iEdge)
          end do
        end do
        ! New layer thickness is after horizontal and vertical thickness flux
        do k = 1, kmax
          hProvInv(k,iCell) = 1.0_RKIND/ hProv(k,iCell)
          hNewInv (k,iCell) = 1.0_RKIND/(hProv(k,iCell) - &
                                         dt*w(k,iCell) + dt*w(k+1, iCell))
        end do
      end do
      !$omp end do
      !$omp end parallel

#ifdef _ADV_TIMERS
      call mpas_timer_stop('startup')
#endif

#ifdef _ADV_TIMERS
      call mpas_timer_start('cell init')
#endif
      ! reset nCells to include all cells
      nCells = nCellsAll

      ! Compute the high and low order horizontal fluxes.
#ifdef _ADV_TIMERS
      call mpas_timer_stop('cell init')
      call mpas_timer_start('horiz flux')
#endif

      ! set nCells to first halo level
      nCells = nCellsHalo( 1 )

      ! Determine bounds on glsPsi (glsPsiMin and glsPsiMax) from
      ! surrounding cells for later limiting.

      !$omp parallel
      !$omp do schedule(runtime) private(k, i, cell2, kmax)
      do iCell = 1, nCells
         do k=1, maxLevelCell(iCell)
            glsPsiMin(k,iCell) = glsPsi(k,iCell)
            glsPsiMax(k,iCell) = glsPsi(k,iCell)
         end do
         do i = 1, nEdgesOnCell(iCell)
            cell2 = cellsOnCell(i,iCell)
            kmax  = min(maxLevelCell(iCell), &
                        maxLevelCell(cell2))
            do k=1, kmax
              glsPsiMax(k,iCell) = max(glsPsiMax(k,iCell), &
                                       glsPsi(k,cell2))
              glsPsiMin(k,iCell) = min(glsPsiMin(k,iCell), &
                                       glsPsi(k,cell2))
            end do ! k loop
         end do ! i loop over nEdgesOnCell
      end do
      !$omp end do
      !$omp end parallel

      ! Need all the edges around the 1 halo cells and owned cells
      nEdges = nEdgesHalo( 2 )

      ! Compute the high order horizontal flux

      !$omp parallel
      !$omp do schedule(runtime) &
      !$omp private(cell1, cell2, k, wgtTmp, sgnTmp, flxTmp, i, iCell, coef1, coef3, &
      !$omp         glsPsiWeight)
      do iEdge = 1, nEdges
         cell1 = cellsOnEdge(1, iEdge)
         cell2 = cellsOnEdge(2, iEdge)

         ! compute some common intermediate factors
         do k = 1, nVertLevels
            wgtTmp(k) = normalThicknessFlux   (k,iEdge)* &
                        highOrderAdvectionMask(k,iEdge)
            sgnTmp(k) = sign(1.0_RKIND, &
                             normalThicknessFlux(k,iEdge))
            flxTmp(k) = 0.0_RKIND
         end do

         ! Compute 3rd or 4th fluxes where requested.
         do i = 1, nAdvCellsForEdge(iEdge)
            iCell = advCellsForEdge(i,iEdge)
            coef1 = advCoefs       (i,iEdge)
            coef3 = advCoefs3rd    (i,iEdge)*coef3rdOrder
            do k = 1, maxLevelCell(iCell)
              flxTmp(k) = flxTmp(k) + glsPsi(k,iCell)* &
                          wgtTmp(k)*(coef1 + coef3*sgnTmp(k))
            end do ! k loop
          end do ! i loop over nAdvCellsForEdge

          do k=1,nVertLevels
             highOrderFlx(k,iEdge) = flxTmp(k)
          end do

          ! Compute 2nd order fluxes where needed.
          ! Also compute low order upwind horizontal flux (monotonic and diffused)
          ! Remove low order flux from the high order flux
          ! Store left over high order flux in highOrderFlx array
          do k = 1, maxLevelEdgeTop(iEdge)
            glsPsiWeight = (1.0_RKIND - highOrderAdvectionMask(k, iEdge)) &
                         * (dvEdge(iEdge) * 0.5_RKIND)                 &
                         * normalThicknessFlux(k, iEdge)

            lowOrderFlx(k,iEdge) = dvEdge(iEdge) * &
               (max(0.0_RKIND,normalThicknessFlux(k,iEdge))*glsPsi(k,cell1) &
              + min(0.0_RKIND,normalThicknessFlux(k,iEdge))*glsPsi(k,cell2))

            highOrderFlx(k, iEdge) = highOrderFlx(k, iedge) &
                                   + glsPsiWeight * (glsPsi(k, cell1) &
                                                   + glsPsi(k, cell2))

            highOrderFlx(k,iEdge) = highOrderFlx(k,iEdge) &
                                  -  lowOrderFlx(k,iEdge)
          end do ! k loop
        end do ! iEdge loop
        !$omp end do
        !$omp end parallel

#ifdef _ADV_TIMERS
        call mpas_timer_stop('horiz flux')
        call mpas_timer_start('scale factor build')
#endif
        ! Initialize flux arrays for all cells
        nCells = nCellsHalo(size(nCellsHalo))

        !$omp parallel
        !$omp do schedule(runtime)
        do iCell = 1, nCells+1
        do k=1, nVertLevels
           workTend(k, iCell) = 0.0_RKIND
           flxIn   (k, iCell) = 0.0_RKIND
           flxOut  (k, iCell) = 0.0_RKIND
        end do ! k loop
        end do ! iCell loop
        !$omp end do
        !$omp end parallel

        ! Need one halo of cells around owned cells
        nCells = nCellsHalo( 1 )

        !$omp parallel
        !$omp do schedule(runtime) &
        !$omp private(invAreaCell1, i, iEdge, cell1, cell2, signedFactor, k, &
        !$omp         tkeUpwindNew, tkeMinNew, tkeMaxNew, scaleFactor)
        do iCell = 1, nCells
          invAreaCell1 = 1.0_RKIND / areaCell(iCell)

          ! Finish computing the low order horizontal fluxes
          ! Upwind fluxes are accumulated in workTend
          do i = 1, nEdgesOnCell(iCell)
            iEdge = edgesOnCell(i, iCell)
            cell1 = cellsOnEdge(1,iEdge)
            cell2 = cellsOnEdge(2,iEdge)
            signedFactor = edgeSignOnCell(i, iCell) * invAreaCell1

            do k = 1, maxLevelEdgeTop(iEdge)
              ! Here workTend is the advection tendency due to the
              ! upwind (low order) fluxes.
              workTend(k,iCell) = workTend(k,iCell) &
                                + signedFactor*lowOrderFlx(k,iEdge)

              ! Accumulate remaining high order fluxes
              flxOut(k,iCell) = flxOut(k,iCell) + min(0.0_RKIND,  &
                                signedFactor*highOrderFlx(k,iEdge))
              flxIn (k,iCell) = flxIn (k,iCell) + max(0.0_RKIND,  &
                                signedFactor*highOrderFlx(k,iEdge))

            end do
          end do

          ! Build the factors for the FCT
          ! Computed using the bounds that were computed previously,
          ! and the bounds on the newly updated value
          ! Factors are placed in the flxIn and flxOut arrays
          do k = 1, maxLevelCell(iCell)
            ! Here workTend is the upwind tendency
            glsPsiUpwindNew = (glsPsi(k,iCell)*layerThickness(k,iCell) &
                             + dt*workTend(k,iCell)) * hProvInv(k,iCell)
            glsPsiMinNew = glsPsiUpwindNew &
                         + dt*flxOut(k,iCell)*hProvInv(k,iCell)
            glsPsiMaxNew = glsPsiUpwindNew &
                         + dt*flxIn (k,iCell)*hProvInv(k,iCell)

            scaleFactor = (glsPsiMax(k,iCell) - glsPsiUpwindNew)/ &
                          (glsPsiMaxNew - glsPsiUpwindNew + eps)
            flxIn (k,iCell) = min(1.0_RKIND, max(0.0_RKIND, scaleFactor))

            scaleFactor = (glsPsiUpwindNew - glsPsiMin(k,iCell))/ &
                          (glsPsiUpwindNew - glsPsiMinNew + eps)
            flxOut(k,iCell) = min(1.0_RKIND, max(0.0_RKIND, scaleFactor))
          end do ! k loop
        end do ! iCell loop
        !$omp end do
        !$omp end parallel

#ifdef _ADV_TIMERS
        call mpas_timer_stop('scale factor build')
        call mpas_timer_start('rescale horiz fluxes')
#endif
        ! Need all of the edges around owned cells
        nEdges = nEdgesHalo( 1 )
        !  rescale the high order horizontal fluxes
        !$omp parallel
        !$omp do schedule(runtime) private(cell1, cell2, k)
        do iEdge = 1, nEdges
          cell1 = cellsOnEdge(1,iEdge)
          cell2 = cellsOnEdge(2,iEdge)
          do k = 1, maxLevelEdgeTop(iEdge)
            highOrderFlx(k,iEdge) = max(0.0_RKIND,highOrderFlx(k,iEdge))* &
                                    min(flxOut(k,cell1), flxIn (k,cell2)) &
                                  + min(0.0_RKIND,highOrderFlx(k,iEdge))* &
                                    min(flxIn (k,cell1), flxOut(k,cell2))
          end do ! k loop
        end do ! iEdge loop
        !$omp end do
        !$omp end parallel
#ifdef _ADV_TIMERS
        call mpas_timer_stop('rescale horiz fluxes')
        call mpas_timer_start('flux accumulate')
#endif

        nCells = nCellsOwned
        ! Accumulate the scaled high order vertical tendencies, and the upwind tendencies
        !$omp parallel
        !$omp do schedule(runtime) private(invAreaCell1, signedFactor, i, iEdge, k)
        do iCell = 1, nCells
          invAreaCell1 = 1.0_RKIND / areaCell(iCell)

          ! Accumulate the scaled high order horizontal tendencies
          do i = 1, nEdgesOnCell(iCell)
            iEdge = edgesOnCell(i, iCell)
            signedFactor = invAreaCell1 * edgeSignOnCell(i, iCell)
            do k = 1, maxLevelEdgeTop(iEdge)
              ! workTend on the RHS is the upwind tendency
              ! workTend on the LHS is the total horizontal advection tendency
              workTend(k,iCell) = workTend(k,iCell) &
                                + signedFactor * highOrderFlx(k, iEdge)
            end do
          end do

          do k = 1, maxLevelCell(iCell)
            ! workTend on the RHS is the total horizontal advection tendency
            ! glsPsi on LHS is the  provisional tke after horizontal fluxes only.
            tend(k,iCell) = tend(k,iCell) + workTend(k,iCell)
          end do

        end do ! iCell loop
        !$omp end do
        !$omp end parallel

#ifdef _ADV_TIMERS
        call mpas_timer_stop('flux accumulate')
#endif
        if (monotonicityCheck) then
           nCells = nCellsOwned
           ! Check tke values against local min,max to detect
           ! non-monotone values and write warning if found
           !$omp parallel
           !$omp do schedule(runtime) private(k)
           do iCell = 1, nCells
           do k = 1, maxLevelCell(iCell)
              if(glsPsi(k,iCell) < glsPsiMin(k, iCell)-eps) then
                 call mpas_log_write( &
                    'Horizontal minimum out of bounds on glsPsi: $r $r ', &
                    MPAS_LOG_WARN,                      &
                    realArgs=(/ glsPsiMin(k, iCell), glsPsi(k,iCell) /) )
              end if

              if(glsPsi(k,iCell) > glsPsiMax(k,iCell)+eps) then
                 call mpas_log_write( &
                    'Horizontal maximum out of bounds on tke: $r $r ', &
                    MPAS_LOG_WARN,                      &
                    realArgs=(/ glsPsiMax(k, iCell), glsPsi(k,iCell) /) )
              end if
           end do
           end do
           !$omp end do
           !$omp end parallel
        end if ! monotonicity check

        !-----------------------------------------------------------------------
        !
        !  Horizontal advection complete
        !  Begin vertical advection
        !
        !-----------------------------------------------------------------------

#ifdef _ADV_TIMERS
        call mpas_timer_start('cell init')
#endif
        nCells = nCellsHalo( size(nCellsHalo) )
        ! Initialize variables for use in this portion of the computation. 
        !$omp parallel
        !$omp do schedule(runtime) private(k)
        do iCell = 1, nCells
          do k=1, nVertLevels
            highOrderFlx(k, iCell) = 0.0_RKIND
            workTend(k, iCell) = 0.0_RKIND
          end do ! k loop
          highOrderFlx(nVertLevels+1, iCell) = 0.0_RKIND

        end do ! iCell loop
        !$omp end do
        !$omp end parallel
#ifdef _ADV_TIMERS
        call mpas_timer_stop('cell init')
#endif

#ifdef _ADV_TIMERS
        call mpas_timer_start('vertical flux')
#endif

        ! Need all owned and 1 halo cells
        nCells = nCellsHalo( 1 )

        ! Determine bounds on glsPsi from neighbor values for limiting
        !$omp parallel
        !$omp do schedule(runtime) private(kmax, k)
        do iCell = 1, nCells
          kmax = maxLevelCell(iCell)

          ! take care of top cell
          glsPsiMax(1,iCell) = max(glsPsi(1,iCell), glsPsi(2,iCell))
          glsPsiMin(1,iCell) = min(glsPsi(1,iCell), glsPsi(2,iCell))
          do k=2,kmax-1
            glsPsiMax(k,iCell) = max(glsPsi(k-1,iCell), &
                                     glsPsi(k  ,iCell), &
                                     glsPsi(k+1,iCell))
            glsPsiMin(k,iCell) = min(glsPsi(k-1,iCell), &
                                     glsPsi(k  ,iCell), &
                                     glsPsi(k+1,iCell))
          end do
          ! finish with bottom cell
          glsPsiMax(kmax,iCell) = max(glsPsi(kmax  ,iCell), &
                                      glsPsi(kmax-1,iCell))
          glsPsiMin(kmax,iCell) = min(glsPsi(kmax  ,iCell), &
                                      glsPsi(kmax-1,iCell))
        end do ! cell loop
        !$omp end do
        !$omp end parallel

        ! Compute the high order vertical fluxes based on order selected.
        ! Special cases for top and bottom layers handled in following loop.

        select case (vertOrder)
        case (vertOrder4)

          !$omp parallel
          !$omp do schedule(runtime) private(kmax, k)
          do iCell = 1, nCells
            kmax = maxLevelCell(iCell)
            do k=3,kmax-1
              highOrderFlx(k, iCell) = w(k,iCell)*( &
                  7.0_RKIND*(glsPsi(k  ,iCell) + glsPsi(k-1,iCell)) - &
                            (glsPsi(k+1,iCell) + glsPsi(k-2,iCell)))/ &
                  12.0_RKIND

            end do
          end do ! cell loop
          !$omp end do
          !$omp end parallel

        case (vertOrder3)

          !$omp parallel
          !$omp do schedule(runtime) private(kmax, k)
          do iCell = 1, nCells
            kmax = maxLevelCell(iCell)
            do k=3,kmax-1
              highOrderFlx(k, iCell) = (w(k,iCell)* &
                   (7.0_RKIND*(glsPsi(k  ,iCell) + glsPsi(k-1,iCell)) - &
                              (glsPsi(k+1,iCell) + glsPsi(k-2,iCell))) - &
                        coef3rdOrder*abs(w(k,iCell))* &
                             ((glsPsi(k+1,iCell) - glsPsi(k-2,iCell)) - &
                    3.0_RKIND*(glsPsi(k  ,iCell) - glsPsi(k-1,iCell))))/ &
                   12.0_RKIND


            end do
          end do ! cell loop
          !$omp end do
          !$omp end parallel

       case (vertOrder2)

          !$omp parallel
          !$omp do schedule(runtime) private(kmax, k, verticalWeightK, verticalWeightKm1)
          do iCell = 1, nCells
            kmax = maxLevelCell(iCell)
            do k=3,kmax-1
              verticalWeightK   = hProv(k-1,iCell) / &
                                 (hProv(k  ,iCell) + hProv(k-1,iCell))
              verticalWeightKm1 = hProv(k  ,iCell) / &
                                 (hProv(k  ,iCell) + hProv(k-1,iCell))
              highOrderFlx(k,iCell) = w(k,iCell)* &
                                 (verticalWeightK  *glsPsi(k  ,iCell) + &
                                  verticalWeightKm1*glsPsi(k-1,iCell))
            end do
          end do ! cell loop
          !$omp end do
          !$omp end parallel

        end select ! vertical order

        ! Treat special cases for high order flux
        ! Compute low order upwind vertical flux (monotonic and diffused)
        ! Remove low order flux from the high order flux.
        ! Store left over high order flux in highOrderFlx array.
        !$omp parallel
        !$omp do schedule(runtime) private(kmax, verticalWeightK, verticalWeightKm1, k)
        do iCell = 1, nCells
          kmax = maxLevelCell(iCell)
          ! Next-to-top cell in column is second-order
          highOrderFlx(1,iCell) = 0.0_RKIND
          if (kmax > 1) then
            verticalWeightK   = hProv(1,iCell) / &
                               (hProv(2,iCell) + hProv(1, iCell))
            verticalWeightKm1 = hProv(2,iCell) / &
                               (hProv(2,iCell) + hProv(1, iCell))
            highOrderFlx(2,iCell) = w(2,iCell)* &
                               (verticalWeightK  *glsPsi(2,iCell) + &
                                verticalWeightKm1*glsPsi(1,iCell))
          end if
          ! Deepest vertical cell in column is second order
          k = max(2,kmax)
          verticalWeightK   = hProv(k-1,iCell) / &
                             (hProv(k  ,iCell) + hProv(k-1,iCell))
          verticalWeightKm1 = hProv(k  ,iCell) / &
                             (hProv(k  ,iCell) + hProv(k-1,iCell))
          highOrderFlx(k,iCell) = w(k,iCell)* &
                             (verticalWeightK  *glsPsi(k  ,iCell) + &
                              verticalWeightKm1*glsPsi(k-1,iCell))
          highOrderFlx(k+1,iCell) = 0.0_RKIND

          ! Compute low order (upwind) flux and remove from high order
          lowOrderFlx(1,iCell) = 0.0_RKIND
          do k = 2, kmax
            lowOrderFlx(k,iCell) = &
                min(0.0_RKIND,w(k,iCell))*glsPsi(k-1,iCell) + &
                max(0.0_RKIND,w(k,iCell))*glsPsi(k  ,iCell)
            highOrderFlx(k,iCell) = highOrderFlx(k,iCell) - &
                                     lowOrderFlx(k,iCell)
          end do ! k loop
          lowOrderFlx(kmax+1,iCell) = 0.0_RKIND

          ! Upwind fluxes are accumulated in workTend
          ! flxIn contains the total remaining high order flux into iCell
          !          it is positive.
          ! flxOut contains the total remaining high order flux out of iCell
          !           it is negative
          do k = 1, kmax
            workTend(k,iCell) = lowOrderFlx(k+1,iCell) &
                              - lowOrderFlx(k  ,iCell)
            flxIn (k, iCell) = max(0.0_RKIND, highOrderFlx(k+1, iCell)) &
                             - min(0.0_RKIND, highOrderFlx(k  , iCell))
            flxOut(k, iCell) = min(0.0_RKIND, highOrderFlx(k+1, iCell)) &
                             - max(0.0_RKIND, highOrderFlx(k  , iCell))
          end do ! k Loop
        end do ! iCell Loop
        !$omp end do
        !$omp end parallel

#ifdef _ADV_TIMERS
        call mpas_timer_stop('vertical flux')
        call mpas_timer_start('scale factor build')
#endif
        ! Need one halo of cells around owned cells
        nCells = nCellsHalo( 1 )
        !$omp parallel
        !$omp do schedule(runtime) &
        !$omp private(k, tkeMinNew, tkeMaxNew, tkeUpwindNew, scaleFactor)
        do iCell = 1, nCells

          ! Build the scale factors to limit flux for FCT
          ! Computed using the bounds that were computed previously,
          ! and the bounds on the newly updated value
          ! Factors are placed in the flxIn and flxOut arrays

          do k = 1, maxLevelCell(iCell)
            ! workTend on the RHS is the upwind tendency
            glsPsiMinNew = (glsPsi(k,iCell)*hProv(k,iCell) &
                         + dt*(workTend(k,iCell)+flxOut(k,iCell))) &
                         * hNewInv(k,iCell)
            glsPsiMaxNew = (glsPsi(k,iCell)*hProv(k,iCell) &
                         + dt*(workTend(k,iCell)+flxIn(k,iCell))) &
                         * hNewInv(k,iCell)
            glsPsiUpwindNew = (glsPsi(k,iCell)*hProv(k,iCell) &
                            + dt*workTend(k,iCell)) * hNewInv(k,iCell)

            scaleFactor = (glsPsiMax(k,iCell)-glsPsiUpwindNew)/ &
                          (glsPsiMaxNew-glsPsiUpwindNew+eps)
            flxIn (k,iCell) = min(1.0_RKIND, max(0.0_RKIND, scaleFactor))

            scaleFactor = (glsPsiUpwindNew-glsPsiMin(k,iCell))/ &
                          (glsPsiUpwindNew-glsPsiMinNew+eps)
            flxOut(k,iCell) = min(1.0_RKIND, max(0.0_RKIND, scaleFactor))
          end do ! k loop
        end do ! iCell loop
        !$omp end do
        !$omp end parallel
#ifdef _ADV_TIMERS
        call mpas_timer_stop('scale factor build')
        call mpas_timer_start('flux accumulate')
#endif

        nCells = nCellsOwned
        ! Accumulate the scaled high order vertical tendencies
        ! and the upwind tendencies
        !$omp parallel
        !$omp do schedule(runtime) private(kmax, k, flux)
        do iCell = 1, nCells
          kmax = maxLevelCell(iCell)
          ! rescale the high order vertical flux
          do k = 2, kmax
            flux =  highOrderFlx(k,iCell)
            highOrderFlx(k,iCell) = max(0.0_RKIND,flux)*   &
                                    min(flxOut(k  ,iCell), &
                                        flxIn (k-1,iCell)) &
                                  + min(0.0_RKIND,flux)*   &
                                    min(flxOut(k-1,iCell), &
                                        flxIn (k  ,iCell))
          end do ! k loop

          do k = 1,kmax
            ! workTend on the RHS is the upwind tendency
            ! workTend on the LHS is the total vertical advection tendency
            workTend(k, iCell) = workTend(k, iCell)       &
                               + (highOrderFlx(k+1,iCell) &
                                - highOrderFlx(k  ,iCell))
            tend(k,iCell) = tend(k,iCell) + workTend(k,iCell)
          end do ! k loop

        end do ! iCell loop
        !$omp end do
        !$omp end parallel

        !normalize final tendency by volume to get right units since glsPsi
        ! is not thickness weighted, requires that advection be called first

        !$omp parallel
        !$omp do schedule(runtime) private(k)
        do iCell=1,nCells
           do k=1,maxLevelCell(iCell)
              tend(k,iCell) = tend(k,iCell) / (hProv(k,iCell)*areaCell(iCell))
           end do
        end do
        !$omp end do
        !$omp end parallel

        ! Compute advection diagnostics and monotonicity checks if requested
#ifdef _ADV_TIMERS
        call mpas_timer_stop('flux accumulate')
        call mpas_timer_start('advect diags')
#endif
        if (monotonicityCheck) then
          nCells = nCellsOwned
          ! Check for monotonicity of new tke value
          !$omp parallel
          !$omp do schedule(runtime) private(k, tkeNew)
          do iCell = 1, nCells
          do k = 1, maxLevelCell(iCell)
             ! workTend on the RHS is the total vertical advection tendency
             glsPsiNew = (glsPsi(k, iCell)*hProv(k, iCell) &
                          + dt*workTend(k, iCell))*hNewInv(k, iCell)

             if (glsPsiNew < glsPsi(k, iCell)-eps) then
                call mpas_log_write( &
                   'Vertical minimum out of bounds on glsPsi: $i $i $r $r ',&
                   MPAS_LOG_WARN, intArgs=(/k, iCell/), &
                   realArgs=(/ glsPsiMin(k, iCell), glsPsiNew /) )
             end if

             if (glsPsiNew > glsPsiMax(k,iCell)+eps) then
                call mpas_log_write( &
                   'Vertical maximum out of bounds on glsPsi: $i $i $r $r ',&
                    MPAS_LOG_WARN, intArgs=(/k, iCell/), &
                    realArgs=(/ glsPsiMax(k, iCell), glsPsiNew /) )
             end if
          end do
          end do
          !$omp end do
          !$omp end parallel
        end if ! monotonicity check
#ifdef _ADV_TIMERS
        call mpas_timer_stop('advect diags')
#endif

#ifdef _ADV_TIMERS
      call mpas_timer_start('deallocates')
#endif
      deallocate(glsPsiMin,    &
                 glsPsiMax,    &
                 hNewInv,      &
                 hProv,        &
                 hProvInv,     &
                 flxIn,        &
                 flxOut,       &
                 workTend,     &
                 lowOrderFlx,  &
                 highOrderFlx)

#ifdef _ADV_TIMERS
      call mpas_timer_stop('deallocates')
#endif

   end subroutine ocn_glsPsi_advection_mono_tend!}}}

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
!
!  routine ocn_glsPsi_advection_mono_init
!
!> \brief MPAS initialize monotonic glsPsi advection tendency with FCT
!> \author Luke Van Roekel
!> \date   updated June 2021
!> \details
!>  This routine initializes monotonic glsPsi advection quantities for
!>  the flux-corrected transport (FCT) algorithm.  Based on the tracer advection
!>  mono routine
!
!-------------------------------------------------------------------------------

   subroutine ocn_glsPsi_advection_mono_init(nHalos, horizAdvOrder,        &
                                             vertAdvOrder, inCoef3rdOrder, &
                                             checkMonotonicity, err)!{{{

      !*** input parameters

      integer, intent(in) :: &
         nHalos               !< [in] number of halos in current simulation
      integer, intent(in) :: &
         horizAdvOrder        !< [in] order for horizontal advection
      integer, intent(in) :: &
         vertAdvOrder         !< [in] order for vertical advection
      real (kind=RKIND), intent(in) :: &
         inCoef3rdOrder       !< [in] coefficient for blending advection orders
      logical, intent(in) :: &
         checkMonotonicity    !< [in] flag to check monotonicity of tracers

      !*** output parameters

      integer, intent(out) :: &
         err                   !< [out] error flag

      ! end of preamble
      !----------------
      ! begin code

      err = 0 ! initialize error code to success

      ! Check that the halo is wide enough for FCT
      if (nHalos < 3) then
         call mpas_log_write( &
            'Monotonic advection cannot be used with less than 3 halos.', &
            MPAS_LOG_CRIT)
         err = -1
      end if

      ! Set blending coefficient if 3rd order horizontal advection chosen
      select case (horizAdvOrder)
      case (2)
         coef3rdOrder = 0.0_RKIND
      case (3)
         coef3rdOrder = inCoef3rdOrder
      case (4)
         coef3rdOrder = 0.0_RKIND
      case default
         coef3rdOrder = 0.0_RKIND
         call mpas_log_write( &
            'Invalid value for horizontal advection order, defaulting to 2',&
            MPAS_LOG_WARN)
      end select ! horizontal advection order

      ! Set vertical advection order
      select case (vertAdvOrder)
      case (2)
         vertOrder = vertOrder2
      case (3)
         vertOrder = vertOrder3
      case (4)
         vertOrder = vertOrder4
      case default
         vertOrder = vertOrder2
         call mpas_log_write( &
            'Invalid value for vertical advection order, defaulting to 2', &
            MPAS_LOG_WARN)
      end select ! vertical advection order

      ! Set flag for checking monotonicity
      monotonicityCheck = checkMonotonicity

   end subroutine ocn_glsPsi_advection_mono_init!}}}

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

end module ocn_glsPsi_advection_mono

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
