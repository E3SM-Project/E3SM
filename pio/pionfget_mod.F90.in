#define __PIO_FILE__ "pionfget_mod.F90"
!>
!! @file 
!! $Revision$
!! $LastChangedDate$
!! @brief Read Routines for non-decomposed NetCDF data.
!<
module pionfget_mod
  use iso_c_binding
#ifdef TIMING
  use perf_mod, only : t_startf, t_stopf      ! _EXTERNAL
#endif
  use pio_kinds, only: i4,r4,r8
  use pio_types, only : file_desc_t, var_desc_t
  use pio_support, only : replace_c_null
  implicit none
  private
!>
!! @defgroup PIO_get_var PIO_get_var
!! @brief Reads non-decomposed data from a NetCDF file
!! @details The get_var interface is provided as a simplified interface to
!!  read variables from a NetCDF format file.   The variable is read on the 
!!  root IO task and broadcast in its entirety to all tasks.  
!<
  public :: get_var
  interface get_var
     module procedure get_var_{DIMS}d_{TYPE}, get_var_vdesc_{DIMS}d_{TYPE}
     !  DIMS 1,2,3,4,5
     module procedure get_vara_{DIMS}d_{TYPE}, get_vara_vdesc_{DIMS}d_{TYPE}
     module procedure get_var1_{TYPE}, get_var1_vdesc_{TYPE}
  end interface

 character(len=*), parameter :: modName='pionfget_mod'

CONTAINS

!>
!! @public
!! @ingroup PIO_get_var
!! @brief Reads non-decomposed fields from a NetCDF file
!! @details
!! @param File @ref file_desc_t
!! @param varid : The netcdf variable identifier
!! @param index :  a multidimensional index that specifies which value to get
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_var1_{TYPE} (File,varid, index, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid, index(:)
    {VTYPE}, intent(out) :: ival


    ierr = get_var1_id_{TYPE} (file%fh, varid, index, ival)

  end function get_var1_{TYPE}

  integer function get_var1_id_{TYPE} (ncid,varid, index, ival) result(ierr)
    integer, intent(in) :: ncid
    integer, intent(in) :: varid, index(:)
    {VTYPE}, intent(out) :: ival
    integer(C_SIZE_T), allocatable :: cindex(:)
    integer :: i, clen
    interface
       integer(C_INT) function PIOc_get_var1_{NCTYPE} (ncid, varid, index, ival) &
            bind(C,name="PIOc_get_var1_{NCTYPE}")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         integer(C_SIZE_T) :: index(*)
         {CTYPE} :: ival
       end function PIOc_get_var1_{NCTYPE}
    end interface

#ifdef TIMING
    call t_startf("pio_get_var1_{TYPE}")
#endif
    clen = size(index)
    allocate(cindex(clen))
    do i=1,clen
       cindex(i)=index(clen-i+1)-1
    end do

    ierr = PIOc_get_var1_{NCTYPE} (ncid, varid-1, cindex, ival)
    deallocate(cindex)
#ifdef TIMING
    call t_stopf("pio_get_var1_{TYPE}")
#endif
  end function get_var1_id_{TYPE}

!>
!! @public
!! @ingroup PIO_get_var
!! @brief Writes an netcdf attribute to a file
!! @details
!! @param File @ref file_desc_t
!! @param vardesc @ref var_desc_t
!! @param index :  a multidimensional index that specifies which value to get
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_var1_vdesc_{TYPE} (File,vardesc, index, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    type(var_desc_t), intent(in) :: vardesc
    integer, intent(in) :: index(:)
    {VTYPE}, intent(out) :: ival

    character(len=*), parameter :: subName=modName//'::get_var1_vdesc_{TYPE}'

    ierr = get_var1_{TYPE} (File, vardesc%varid, index, ival)

  end function get_var1_vdesc_{TYPE}


! TYPE int,real,double
! DIMS 1,2,3,4,5
!>
!! @public
!! @ingroup PIO_get_var
!! @brief Writes an netcdf attribute to a file
!! @details
!! @param File @ref file_desc_t
!! @param varid : The netcdf variable identifier
!! @param start : A vector of size_t integers specifying the index in
!!   the variable where the first of the data values will be read. The
!!   indices are relative to 0, so for example, the first data value of
!!   a variable would have index (0, 0, ... , 0). The length of start
!!   must be the same as the number of dimensions of the specified
!!   variable. The elements of start correspond, in order, to the
!!   variable's dimensions. Hence, if the variable is a record variable,
!!   the first index would correspond to the starting record number for
!!   reading the data values.
!! @param count : A vector of size_t integers specifying the edge
!!   lengths along each dimension of the block of data values to be
!!   read. To read a single value, for example, specify count as (1, 1,
!!   ... , 1). The length of count is the number of dimensions of the
!!   specified variable. The elements of count correspond, in order, to
!!   the variable's dimensions. Hence, if the variable is a record
!!   variable, the first element of count corresponds to a count of the
!!   number of records to read.
!! Note: setting any element of the count array to zero causes the function to exit without error, and without doing anything. 
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_vara_{DIMS}d_{TYPE} (File,varid, start, count, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid, start(:), count(:)
    {VTYPE}, intent(out) :: ival{DIMSTR}

    ierr = get_vara_{TYPE}_internal (File%fh, varid, start, count, ival)

  end function get_vara_{DIMS}d_{TYPE}

! DIMS 1,2,3,4,5
  integer function get_vara_{DIMS}d_text (File,varid, start, count, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid, start(:), count(:)
    character(len=*), intent(out) :: ival{DIMSTR}

    ierr = get_vara_text_internal (File%fh, varid, start, count, size(ival), ival)

  end function get_vara_{DIMS}d_text

! DIMS 1,2,3,4,5
!>
!! @public
!! @ingroup PIO_get_var
!! @brief Writes an netcdf attribute to a file
!! @details
!! @param File @ref file_desc_t
!! @param vardesc @ref var_desc_t
!! @param start : A vector of size_t integers specifying the index in
!!   the variable where the first of the data values will be read. The
!!   indices are relative to 0, so for example, the first data value of
!!   a variable would have index (0, 0, ... , 0). The length of start
!!   must be the same as the number of dimensions of the specified
!!   variable. The elements of start correspond, in order, to the
!!   variable's dimensions. Hence, if the variable is a record variable,
!!   the first index would correspond to the starting record number for
!!   reading the data values.
!! @param count : A vector of size_t integers specifying the edge
!!   lengths along each dimension of the block of data values to be
!!   read. To read a single value, for example, specify count as (1, 1,
!!   ... , 1). The length of count is the number of dimensions of the
!!   specified variable. The elements of count correspond, in order, to
!!   the variable's dimensions. Hence, if the variable is a record
!!   variable, the first element of count corresponds to a count of the
!!   number of records to read.
!! Note: setting any element of the count array to zero causes the function to exit without error, and without doing anything. 
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_vara_vdesc_{DIMS}d_{TYPE} (File,vardesc, start, count, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    type(var_desc_t), intent(in) :: vardesc
    integer, intent(in) :: start(:), count(:)
    {VTYPE}, intent(out) :: ival{DIMSTR}

    character(len=*), parameter :: subName=modName//'::get_vara_vdesc_{DIMS}d_{TYPE}'

    ierr = get_vara_{DIMS}d_{TYPE} (File, vardesc%varid, start, count, ival)

  end function get_vara_vdesc_{DIMS}d_{TYPE}

!>
!! @public
!! @ingroup PIO_get_var
!! @brief Writes an netcdf attribute to a file
!! @details
!! @param File @ref file_desc_t
!! @param varid : The netcdf variable identifier
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_var_0d_text (File,varid, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid
    character(len=*), intent(out) :: ival

    interface
       integer(C_INT) function PIOc_get_var_text (ncid, varid, ival) &
            bind(C,name="PIOc_get_var_text")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         character(C_CHAR) :: ival(*)
       end function PIOc_get_var_text
    end interface
    ierr = PIOc_get_var_text(File%fh, varid-1, ival)
    call replace_c_null(ival)

  end function Get_var_0d_text


! TYPE int,real,double
! DIMS 0
  integer function get_var_0d_{TYPE} (File,varid, ival) result(ierr)
    use iso_c_binding
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid
    {VTYPE}, intent(out), target :: ival
    interface
       integer(C_INT) function PIOc_get_var_{NCTYPE} (ncid, varid, ival) &
            bind(C,name="PIOc_get_var_{NCTYPE}")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         type(C_PTR), value :: ival
       end function PIOc_get_var_{NCTYPE}
    end interface
    ierr = PIOc_get_var_{NCTYPE} (File%fh, varid-1, C_LOC(ival))
       
  end function Get_var_0d_{TYPE}

! DIMS 1,2,3,4,5
  integer function get_var_{DIMS}d_text (File,varid, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid
    character(len=*), intent(out) :: ival{DIMSTR}

    ierr = get_var_text_internal(File%fh, varid, size(ival), ival)

  end function get_var_{DIMS}d_text

  integer function get_var_text_internal (ncid,varid, nstrs, ival) result(ierr)
    integer, intent(in) :: ncid
    integer, intent(in) :: varid
    integer, intent(in) :: nstrs
    character(len=*), intent(out) :: ival(*)
    integer :: j
    interface
       integer(C_INT) function PIOc_get_var_text (ncid, varid, ival) &
            bind(C,name="PIOc_get_var_text")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         character(C_CHAR) :: ival(*)
       end function PIOc_get_var_text
    end interface

    ierr = PIOc_get_var_text(ncid, varid-1, ival)
    do j=1,nstrs
       call replace_c_null(ival(j))
    end do

  end function get_var_text_internal

! TYPE int,real,double
  integer function get_var_{TYPE}_internal  (ncid,varid, ival) result(ierr)
    integer, intent(in) :: ncid
    integer, intent(in) :: varid
    {VTYPE}, intent(out), target :: ival(*)
    interface
       integer(C_INT) function PIOc_get_var_{NCTYPE} (ncid, varid, ival) &
            bind(C,name="PIOc_get_var_{NCTYPE}")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         type(C_PTR), value  :: ival
       end function PIOc_get_var_{NCTYPE}
    end interface
    ierr = PIOc_get_var_{NCTYPE} (ncid, varid-1, C_LOC(ival))

  end function get_var_{TYPE}_internal

! TYPE int,real,double
  integer function get_vara_{TYPE}_internal  (ncid,varid, start, count, ival) result(ierr)
    integer, intent(in) :: ncid
    integer, intent(in) :: varid
    integer, intent(in) :: start(:)
    integer, intent(in) :: count(:)
    {VTYPE}, intent(out) :: ival(*)
    integer(C_SIZE_T), allocatable :: cstart(:), ccount(:)
    integer :: i, ndims
    interface
       integer(C_INT) function PIOc_get_vara_{NCTYPE} (ncid, varid, start, count, ival) &
            bind(C,name="PIOc_get_vara_{NCTYPE}")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         integer(C_SIZE_T)  :: start(*)
         integer(C_SIZE_T)  :: count(*)
         {CTYPE}  :: ival(*)
       end function PIOc_get_vara_{NCTYPE}
    end interface
    ndims = size(start)
    allocate(cstart(ndims),ccount(ndims))
    do i=1,ndims
       cstart(i) = start(ndims-i+1)-1
       ccount(i) = count(ndims-i+1)
    enddo

    ierr = PIOc_get_vara_{NCTYPE} (ncid, varid-1, cstart, ccount, ival)
    deallocate(cstart, ccount)

  end function get_vara_{TYPE}_internal

  integer function get_vara_text_internal  (ncid,varid, start, count, nstrs, ival) result(ierr)
    integer, intent(in) :: ncid
    integer, intent(in) :: varid
    integer, intent(in) :: start(:)
    integer, intent(in) :: count(:)
    integer, intent(in) :: nstrs
    character, intent(out) :: ival(*)
    integer :: j
    integer(C_SIZE_T), allocatable :: cstart(:), ccount(:)
    integer :: i, ndims
    interface
       integer(C_INT) function PIOc_get_vara_text (ncid, varid, start, count, ival) &
            bind(C,name="PIOc_get_vara_text")
         use iso_c_binding
         integer(C_INT), value :: ncid
         integer(C_INT), value :: varid
         integer(C_SIZE_T)  :: start(*)
         integer(C_SIZE_T)  :: count(*)
         character(C_CHAR)  :: ival(*)
       end function PIOc_get_vara_text
    end interface
    ndims = size(start)
    allocate(cstart(ndims),ccount(ndims))
    do i=1,ndims
       cstart(i) = start(ndims-i+1)-1
       ccount(i) = count(ndims-i+1)
    enddo

    ierr = PIOc_get_vara_text (ncid, varid-1, cstart, ccount, ival)
    deallocate(ccount, cstart)
    do j=1,nstrs
       call replace_c_null(ival(j))
    end do
  end function get_vara_text_internal

! TYPE int,real,double
! DIMS 1,2,3,4,5
  integer function get_var_{DIMS}d_{TYPE} (File,varid, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    integer, intent(in) :: varid
    {VTYPE}, intent(out) :: ival{DIMSTR}

    ierr = get_var_{TYPE}_internal(File%fh, varid, ival)

  end function get_var_{DIMS}d_{TYPE}


!>
!! @public
!! @ingroup PIO_get_var
!! @brief Writes an netcdf attribute to a file
!! @details
!! @param File @ref file_desc_t
!! @param vardesc @ref var_desc_t
!! @param ival : The value for the netcdf metadata
!! @retval ierr @ref error_return
!<
  integer function get_var_vdesc_{DIMS}d_{TYPE} (File,vardesc, ival) result(ierr)
    type (File_desc_t), intent(in) :: File
    type(var_desc_t), intent(in) :: vardesc
    {VTYPE}, intent(out) :: ival{DIMSTR}

    ierr = get_var_{DIMS}d_{TYPE} (File, vardesc%varid, ival)

  end function get_var_vdesc_{DIMS}d_{TYPE}
end module pionfget_mod
