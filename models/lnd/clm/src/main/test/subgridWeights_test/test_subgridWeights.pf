module test_subgridWeights

  ! Tests of subgridWeightsMod

  use pfunit_mod
  use unittestSubgridMod
  use subgridWeightsMod
  use shr_kind_mod, only : r8 => shr_kind_r8

  implicit none
  save

  real(r8), parameter :: tol = 1.e-14_r8

contains
  
  subroutine setup()
    call unittest_subgrid_setup_start()

    call unittest_add_gridcell()
    call unittest_add_landunit(my_gi=gi, ltype=1, wtgcell=0.25_r8)
    call unittest_add_landunit(my_gi=gi, ltype=3, wtgcell=0.75_r8)

    call unittest_subgrid_setup_end()
  end subroutine setup
  
  subroutine teardown()
    call unittest_subgrid_teardown()
  end subroutine teardown

  ! ------------------------------------------------------------------------
  ! Tests of get_landunit_weight
  ! ------------------------------------------------------------------------

  @Test
  subroutine test_get_landunit_weight_no_landunit()
    ! no landunit of the given type
    call setup()
    @assertEqual(0._r8, get_landunit_weight(gi, 2))
    call teardown()
  end subroutine test_get_landunit_weight_no_landunit

  @Test
  subroutine test_get_landunit_weight_normal()
    ! normal case, with a landunit of the given type
    call setup()
    @assertEqual(0.75_r8, get_landunit_weight(gi, 3), tolerance=tol)
    call teardown()
  end subroutine test_get_landunit_weight_normal

  ! ------------------------------------------------------------------------
  ! Tests of set_landunit_weight
  ! ------------------------------------------------------------------------
  
  @Test
  subroutine test_set_landunit_weight()
    call setup()
    call set_landunit_weight(gi, 3, 0.42_r8)
    @assertEqual(0.42_r8, get_landunit_weight(gi, 3), tolerance=tol)
    call teardown()
  end subroutine test_set_landunit_weight

  ! ------------------------------------------------------------------------
  ! Tests of is_gcell_all_ltypeX
  ! ------------------------------------------------------------------------
  
  @Test
  subroutine test_is_gcell_all_ltypeX_false()
    ! test with no landunit being 100%
    call setup()
    @assertFalse(is_gcell_all_ltypeX(gi, 3))
    call teardown()
  end subroutine test_is_gcell_all_ltypeX_false
  
  @Test
  subroutine test_is_gcell_all_ltypeX_true()
    ! test with a landunit being 100%
    call setup()
    call set_landunit_weight(gi, 1, 0.0_r8)
    call set_landunit_weight(gi, 3, 1.0_r8)
    @assertFalse(is_gcell_all_ltypeX(gi, 1))
    @assertTrue(is_gcell_all_ltypeX(gi, 3))
    call teardown()
  end subroutine test_is_gcell_all_ltypeX_true

end module test_subgridWeights
