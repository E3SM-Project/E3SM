#! /bin/csh -f


#################################################################################
if ($PHASE == set_batch) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1

set mppsize = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
echo $mppsize
@ mppnodes = $mppsize / ${MAX_TASKS_PER_NODE}
if ( $mppsize < ${MAX_TASKS_PER_NODE} ) then 
  set ppn = $mppsize
else
  set ppn = ${MAX_TASKS_PER_NODE}
endif

if ( $mppsize % ${MAX_TASKS_PER_NODE} > 0) then
  @ mppnodes = $mppnodes + 1
  @ mppsize = $mppnodes * ${MAX_TASKS_PER_NODE}
endif

#--- Job name is first fifteen characters of case name ---
set jobname = `echo ${CASE} | cut -c1-15`
set walltime = "48:00:00"

if ($?TESTMODE) then
 set file = $CASEROOT/${CASE}.test 
else
 set file = $CASEROOT/${CASE}.run 
endif

cat >! $file << EOF1
#!/bin/csh -f
#PBS -N ${jobname}
#PBS -q esd08q
#PBS -l nodes=${mppnodes}:ppn=$ppn
#PBS -l walltime=${walltime}
#PBS -j oe
#PBS -S /bin/csh -V
EOF1


#################################################################################
else if ($PHASE == set_exe) then
#################################################################################

set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
set myaprun = `${CASEROOT}/Tools/taskmaker.pl -mpirun`

set mppsize = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
echo $mppsize
cat >> ${CASEROOT}/${CASE}.run << EOF1
sleep 25
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 
setenv OMP_NUM_THREADS ${maxthrds}
echo \$PBS_NODEFILE -np ${mppsize}
if (\$MPILIB != "mpi-serial") then
   /projects/cesm/devtools/mpich-3.0.4-gcc4.8.1/bin/mpirun -f \$PBS_NODEFILE -np ${mppsize} \$EXEROOT/cesm.exe >&! cesm.log.\$LID
else
   \$EXEROOT/cesm.exe >&! cesm.log.\$LID
endif
wait
echo "\`date\` -- CSM EXECUTION HAS FINISHED" 
EOF1


#################################################################################
else if ($PHASE == set_larch) then
#################################################################################

cat >! $CASEROOT/${CASE}.l_archive << EOF1
#! /bin/csh -f
#PBS -N ${CASE}.l_archive
#PBS -q esd08q
#PBS -l nodes=1
#PBS -l walltime=01:30:00
#PBS -j oe
#PBS -S /bin/csh -V

cd $CASEROOT 
source ./Tools/ccsm_getenv || exit -1
cd \$DOUT_S_ROOT
$CASEROOT/Tools/lt_archive.sh -m copy_dirs_hsi
exit 0

EOF1
chmod 775 ${CASEROOT}/${CASE}.l_archive


#################################################################################
else
#################################################################################

    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################
