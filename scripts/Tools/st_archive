#!/usr/bin/env perl

#-----------------------------------------------------------------------------------------------
# st_archive - script to handle short term archiving 
#-----------------------------------------------------------------------------------------------

use strict;
use Cwd qw(abs_path);
use English;
use Getopt::Long;
use IO::File;
use IO::Handle;
use XML::LibXML;
use Data::Dumper;
use POSIX qw(strftime);
use File::Path;
use File::Basename;
use File::Copy;
use File::Find;
use File::stat;
use File::Glob;

# the st_archive is run in the CASEROOT and needs to look in the CASEROOT/Tools for SetupTools.pm
my $cimeroot  = `./xmlquery CIMEROOT  -value`;
my @dirs = ("$cimeroot/utils/perl5lib" );

unshift @INC, @dirs;
require Config::SetupTools;
require Log::Log4perl;

# Globals
my %opts = (loglevel=>"INFO");
my $compmap;
my $level = Log::Log4perl::Level::to_priority($opts{loglevel});
Log::Log4perl->easy_init({level=>$level,
			  layout=>'%m%n'});

my $logger = Log::Log4perl::get_logger();

#-----------------------------------------------------------------------------------------------
# Setting autoflush (an IO::Handle method) on STDOUT helps in debugging.  It forces the test
# descriptions to be printed to STDOUT before the error messages start.
#-----------------------------------------------------------------------------------------------

*STDOUT->autoflush();                  

# the global config hash holds all valid XML variables necessary for the short term st_archive to function
my %config = SetupTools::getAllResolved();

#-----------------------------------------------------------------------------------------------
# print out the command line usage
#-----------------------------------------------------------------------------------------------
sub usage {
    my $usgstatement;
    $usgstatement = <<EOF;

SYNOPSIS

    Controls short term archiving tasks after the model run is
    successfully completed.  It is called after the case.run script
    successfully completes and the DOUT_S variable is set to TRUE. It
    can also be run as a stand-alone script.

CESM OUTPUT FILE NAMING CONVENTIONS

    The st_archive script adheres to the CESM model file naming
    conventions defined in
    http://www.cesm.ucar.edu/models/cesm2.0/filename_conventions_cesm.html

ENV_ARCHIVE.XML SCHEMA AND DOCUMENT TYPE DEFINITION (DTD)

    The st_archive script depends on the env_archive.xml file in the
    \$CASEROOT directory created by create_newcase. The
    env_archive.xml file can be edited by hand in a standard text
    editor to provide directives for controlling the behavior of the
    short term archiver.

    You can view the env_archive.xml file and check the XML validity
    after editing by running "\$CASEROOT/st_archive -input".

    The attributes in the <comp_archive_spec> XML tag definitions are
    used to define how the files in the \$RUNDIR are distributed into
    corresponding subdirectories in the \$DOUT_S_ROOT archive
    locations and whether or not copies of files with specified
    suffices are preserved in the RUNDIR in addition to being copied
    to the DOUT_S_ROOT archive locations.

    There are multiple <comp_archive_spec> markup tags defined with
    nested <file_extension> elements included as part of a well formed
    XML <comp_archive_spec> markup tag defintion.

    When making changes to the env_archive.xml file in a \$CASEROOT,
    it is recommended that the XML be validated against the xml schema
    defintion document in the
    \$CIMEROOT/cime_config/xml/schemas/archive.xsd. For systems that
    have the xmllint utility installed, execute the following:

    > cd \$CASEROOT
    > xmllint -schema \$CIMEROOT/cime_config/xml/schemas/archive.xsd env_archive.xml

    If the env_archive.xml file contains valid XML code, then output
    from this command should be a complete listing of the
    env_archive.xml file followed by the line:

    env_archive.xml validates Any errors in the XML will be reported
    by the xmllint command and should be resolved before running the
    st_archive script in either the case run script or as a
    stand-alone script.

    The XML markup tag defintions in < >, data type definitions
    denoted in [ ], and "|" separated valid options in ( ) for the
    env_archive.xml file are defined as follows:

    <\?xml version="1.0"\?>

    <config_definition>
    <components>

      <comp_archive_spec name="[regular expression string]
      (cpl|dart|cam\\*|datm|clm\\?|dlnd|rtm|cice|dice|pop|docn|cism|ww3|dwav)">
      Model component name (note: cam\\* and clm\\? are
      historical for CAM and CLM model components).

        <rootdir>[string] (rest|cpl|dart|atm|lnd|rof|ice|ocn|glc|wav)</rootdir> 
        Component root directory name to be created under the
        $\DOUT_S_ROOT location.  These may not correspond to the model
        component name.

        <multi_instance>[string] (true|false)</multi_instance> 
        True or false if model component supports multiple instances
	in a given run directory.

        <files> <file_extension suffix="[regular expression string]">
	Perl regular expression specification for "globbing" filenames
	for matching. Run the command "\$CASEROOT/st_archive -input"
	command to see the list of valid suffices. If the suffix
	begins with a "." then all files matching *.(suffix) are
	included in the globbing. If the suffix begins with a
	character other than "." then all files matching exactly the
	(suffix) are included in the globbing.

            <subdir>[string] (logs|rest|hist|init|diag)</subdir> 
	    subdirectory name to be created under the
	    \$DOUT_S_ROOT/rootdir location

	    <keep_last_in_rundir>[string] (true|false)</keep_last_in_rundir>
            Option to specify whether or not to keep the most
            recent copy of the file with matching suffix in the RUNDIR
            in addition to copying it to the
            \$DOUT_S_ROOT/rootdir/subdir location.

				   
ADDITIONAL XML / ENVIRONMENT VARIABLES USED BY THE SHORT TERM ARCHIVER
THAT ARE INCLUDED IN THE ENV_RUN.XML FILE

    The following xml variables read from the \$CASEROOT/env_run.xml
    file are used to control the behavior of the short term archiver.
    They can be queried using the xmlquery tool and modified using
    the xmlchange tool.

    DOUT_S - [boolean] Checked in \$CASE.run script. If TRUE
    (default), then st_archive script called at the end of a job.

    DOUT_S_ROOT - [string] Valid directory path to stage short term
    archive files. The st_archive creates an archive locked directory
    (ARCHIVE_DIR_LOCKED) using the basename of the DOUT_S_ROOT location.

    DOUT_S_SAVE_ALL_ON_DISK - [boolean] If set to TRUE (default), then
    the st_archive creates an archive linked directory
    (\$ARCHIVE_DIR_LINKED) using the basename of the \$DOUT_S_ROOT
    location.  The \$ARCHIVE_DIR_LOCKED and \$ARCHIVE_DIR_LINKED
    directories reside on the same disk volume because hard links are
    used to minimize disk space during a run. Each directory contains
    a README file with details about how to best use these
    directories. The permissions on the archive directories are set
    using the umask settings of the user running the CESM run script.
    This option is also used by the long term archiver script to save
    all short term archived output files on disk rather than deleting
    them after archival to HPSS.

    DOUT_S_SAVE_INTERIM_RESTART_FILES - [boolean] If TRUE (default
    FALSE), perform short term archiving on all interim restart files,
    not just those at the end of the run. By default, this value is
    FALSE.  The restart files are saved under the specific component
    directory of \$DOUT_S_ROOT
    (\$ARCHIVE_DIR_L*/\$CASE/[comp_archive_spec/rootdir]/rest rather
    than the top-level \$ARCHIVE_DIR_L*/\$CASE/rest directory).
    Interim restart files are created using the \$REST_N and
    \$REST_OPTION variables.  The associated rpointer files are not
    saved with the interim restart files and need to be manually
    generated in order for these restart files to be used to restart a
    run. This is for expert users ONLY and requires expert
    knowledge. We will not document this further in this guide.

    DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET - [integer] If value is
    greater than 0 (default), then only save the nth restart set in
    the \$DOUT_S_ROOT/rest location. Always preserve the most recent
    restart set regardless.

USAGE
    st_archive [options]

OPTIONS
    -help [or -h] Print usage to STDOUT.

    -input List out the contents of the env_archive.xml datafile in a
           friendly format and check the env_archive.xml file for
           validity.

    -output List out the contents of the archive directory.

    -clean Create / update hardlinks in both the \$ARCHIVE_DIR_LOCKED
           and \$ARCHIVE_DIR_LINKED for duplicate files.

    -link Create / update the hardlinks from the \$ARCHIVE_DIR_LINKED
          to the \$ARCHIVE_DIR_LOCKED.

    -undo move all the files from the \$ARCHIVE_DIR_LOCKED back into the \$RUNDIR

    - loglevel - set level of output from this script DEBUG, INFO, WARN, ERROR, FATAL (default INFO)

EOF
#" added per for emacs perl mode parsing

    $logger->info($usgstatement);
    exit(1);
}


#-----------------------------------------------------------------------------------------------
# Parse command-line options.
#-----------------------------------------------------------------------------------------------
sub getOptions
{
    GetOptions(
	"h|help"        => \$opts{'help'},
        "in|input"      => \$opts{'input'}, 	
        "out|output"    => \$opts{'output'}, 	
        "clean"         => \$opts{'clean'}, 	
        "link"          => \$opts{'link'}, 	
	"undo"          => \$opts{'undo'},
	"loglevel"     => \$opts{loglevel}
    );
    usage() if $opts{'help'};
}


#-----------------------------------------------------------------------------------------------
# check the run environment and create the associated directories
#-----------------------------------------------------------------------------------------------
sub checkRun
{
    my $statusFile = shift;
    my $runComplete = 0;

    $logger->debug("In checkRun..."); 
#
# check if DOUT_S_ROOT is defined
#
    if( !defined $config{'DOUT_S_ROOT'} || uc($config{'DOUT_S_ROOT'}) eq 'UNSET' )
    {
	$logger->logdie("st_archive: Error - XML variable DOUT_S_ROOT is required for root location of short-term achiver. Exiting...");
    }
#
# check if DOUT_S_ROOT dir needs to be created (or not) and if it can be read - creation permissions defaults to user's umask setting
# force the output filename to have .locked appended to distinguish from .linked 
#
    my $dir = dirname( $config{'DOUT_S_ROOT'} );
    my $basename = basename( $config{'DOUT_S_ROOT'} );
    $config{'ARCHIVE_DIR_LOCKED'} = qq($dir.locked/$basename);

    my $readme = qq($dir.locked/README.locked);
    mkpath( $config{'ARCHIVE_DIR_LOCKED'}, {verbose => 1, error => \my $err_list} ) unless ( -d $config{'ARCHIVE_DIR_LOCKED'} );
    chdir( $config{'ARCHIVE_DIR_LOCKED'} ) or $logger->logdie ("st_archive: Error - cannot access directory $config{'ARCHIVE_DIR_LOCKED'} with error $err_list. Exiting...");

    if( !-e $readme ) {
	open ( my $fh, ">", $readme) or $logger->logdie("st_archive: Error - cannont create $readme. Exiting...");
	print $fh <<"EOD";

** IMPORTANT NOTICE ** 
This directory is actively used by the CESM model when a job is running. 
It is strongly recommended that users *NEVER* run diagnostics or
post-processing packages in this location while the model is running 
and the short term st_archive is active. 

A parallel directory located in DOUT_S_ROOT is available for *READ ONLY* 
operations on CESM model output files. The short term archiver (st_archive)
automatically creates and populates this directory with hard linked filenames 
when the environment variable DOUT_S_SAVE_ALL_ON_DISK is set to TRUE. Users 
may run diagnostics and post-processing routines in this location while
the model is running and st_archive is also running. However,
be aware that the file names in this directory may not change but
the content of the files themselves may change while st_archive is active. 
This is because hard links are used to point to a specific location 
on disk using the same filenames as those in the DOUT_S_ROOT.locked directory.

EOD
;
	close($fh);
    }
#
# check if interim restart files should be saved - if not, print a warning
#
    if( uc($config{'DOUT_S_SAVE_INTERIM_RESTART_FILES'}) eq 'FALSE' || uc($config{'DOUT_S_SAVE_INTERIM_RESTART_FILES'}) eq 'UNSET' ) 
    {
	$config{'DOUT_S_SAVE_INTERIM_RESTART_FILES'} = 'FALSE';
	$logger->warn("st_archive: Warning - restart files from end of run will be saved, interim restart files will be deleted.");
    }
#
# check if the run completed successfully
#
    if( -f $statusFile ) 
    {
	open my $CASESTATUS, "<", "$statusFile"  or die qq(st_archive: unable to open $statusFile. Exiting...\n);
	while( <$CASESTATUS> ) 
	{
	    chomp $_;
	    if( /^run SUCCESSFUL/ )
	    {
		$runComplete = 1;
	    }
	}
	close( $CASESTATUS );
    }
    if( !defined $config{'DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET'} || uc($config{'DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET'}) eq 'UNSET' )
    {
	$config{'DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET'} = 0;
    }

    return $runComplete;
}


#-----------------------------------------------------------------------------------------------
# read the archive XML file - env_archive.xml
#-----------------------------------------------------------------------------------------------
sub readXMLin
{
    my $filename = shift;
    $logger->debug("In readXMLin..."); 
    my $parser = XML::LibXML->new();
    my $xml = $parser->parse_file($filename);
    return $xml;
}

#-----------------------------------------------------------------------------------------------
# list archive XML file contents
#-----------------------------------------------------------------------------------------------
sub listXMLin
{
    my ($xml) = shift;
    $logger->debug("In listXMLin..."); 
    my $rootel = $xml->getDocumentElement();

    my $schema = <<EOF;

The env_archive.xml brief XML tag schema:

    <comp_archive_spec name="[string:component name] (cpl|dart|cam\*|datm|clm\?|dlnd|rtm|cice|dice|pop|docn|cism|ww3|dwav)">
	<rootdir>[string:root directory name after DOUT_S_ROOT.locked] (rest|cpl|dart|atm|lnd|rof|ice|ocn|glc|wav)</rootdir>
        <multi_instance>[string: multi-instance support] (true|false)</multi_instance>
        <files>
            <file_extension suffix="[string: perl regular expression for matching file suffix patterns"]>
	    <keep_last_in_rundir>[string: specify if the most recent copy of the matching file should be saved in the RUNDIR] (true|false)</keep_last_in_rundir> 
        </files>
    </comp_archive_spec>
			
Run st_archive --help for complete tag schema descriptions.	   

EOF
# added for emacs perl mode parsing

    print $schema;

    my $elname = $rootel->getName();
    $logger->info("Root element is a $elname and it contains ...");
#
# list out the components nodes
#
    my @comps = ($xml->findnodes('//comp_archive_spec'));
    foreach my $comp (@comps) {

	my $compname = $comp->getAttribute('name');
	my $rootdir = $comp->findvalue('./rootdir');
#	my $multi = $comp->findvalue('./multi_instance');
	my $ninst = getcompninst($compname);
	my $multi = "false";
	if($ninst > 1){
	    $multi = "true";
	}
	

	$logger->info("\n============================================================================");
	$logger->info("Component name = $compname");
	$logger->info("rootdir = $rootdir");
	$logger->info("multiple-instance support = $multi");

	my @files = $comp->findnodes('./files/file_extension');
        foreach my $file (@files) 
	{
	    my $suffix = $file->getAttribute('suffix');
	    my $subdir = $file->findvalue('./subdir');
	    my $keep_last_in_rundir = $file->findvalue('./keep_last_in_rundir');

	    $logger->info("\n***** File extension specification");
	    $logger->info("  suffix = $suffix");  
	    $logger->info("  subdir = $config{'ARCHIVE_DIR_LOCKED'}/$config{'CASENAME'}$rootdir/$subdir");
	    $logger->info("  keep_last_in_rundir = $keep_last_in_rundir");
	}
    }
}

#-----------------------------------------------------------------------------------------------
# get the date name from the most current coupler restart file
#-----------------------------------------------------------------------------------------------
sub getDname
{
    $logger->debug("In getDname...");

    my $DIR = $config{'RUNDIR'};
    opendir( my $dh, $DIR ) or $logger->logdie("st_archive: Error - cannot open directory $DIR. Exiting...");

    my @files = sort { $b->[10] <=> $a->[10] } 
                map {[ $_, CORE::stat "$DIR/$_" ]} 
                grep m/^*\.cpl\.r\.*/, readdir $dh;
    closedir $dh;

    my ($name, @stat) = @{$files[0]};
    $name =~ s/\.nc//;
    $name =~ s/^.*\.r\.//;

    $logger->debug("Cpl dName: $name"); 
    return $name;
}

#-----------------------------------------------------------------------------------------------
# recursive file tree traverse function to print out the archive
#-----------------------------------------------------------------------------------------------
sub listArchive
{
    my ($thing) = @_;
    $logger->debug("In listArchive: $thing ..."); 

    $logger->info("$thing ");
    return if not -d $thing;
    opendir my $dh, $thing or die;
    while (my $sub = readdir $dh) {
	next if $sub eq '.' or $sub eq '..';
 	listArchive( "$thing/$sub" );
    }
    closedir $dh;
    return;
}


#-----------------------------------------------------------------------------------------------
# recursive file tree traverse function to create the hardlinks
#-----------------------------------------------------------------------------------------------
sub traverse
{
    my ($thing) = @_;
    $logger->debug("In traverse: $thing ...");
# need to check for -f 
    if( -f $thing ) 
    {
# this is a file so create the hardlink
	my $lockfile = my $linkfile = $thing;
	$linkfile =~ s/^\./$config{'ARCHIVE_DIR_LINKED'}/;
	$lockfile =~ s/^\./$config{'ARCHIVE_DIR_LOCKED'}/;
	unlink ($linkfile);
	link ($lockfile, $linkfile);
	return;
    }
    elsif ( -d $thing )
    {
# this is a directory so make sure it exists
	my $linkdir = $thing;
	$linkdir =~  s/^\./$config{'ARCHIVE_DIR_LINKED'}/;
	mkpath( $linkdir, {verbose => 1, error => \my $err_list} )  unless ( -d $linkdir );	

# return if $thing isn't a directory
	opendir my $dh, $thing or $logger->logdie("st_archive: Error - in traverse cannot open directory $thing. Exiting...");
	while (my $sub = readdir $dh) {
	    next if $sub eq '.' or $sub eq '..';
 	    traverse("$thing/$sub");
        }
        closedir $dh;
    }

    return;
}


#-----------------------------------------------------------------------------------------------
# get the NINST_[comp] from the config hash to determine if multiple instances are supported
#-----------------------------------------------------------------------------------------------
sub getcompninst
{
    my ($comp) = @_;

    $comp = substr($comp,0,3);


    if(! defined $compmap){
	foreach( qw(ATM CPL OCN WAV GLC ICE ROF LND )){
	    if($comp = $config{"COMP_".$_}){
		$compmap->{$comp} = $_;
	    }
	}
    }
    my $ninstname = "NINST_".$compmap->{$comp};
    $logger->debug("Looking for $ninstname in config");
    if(defined $config{$ninstname}){
	return $config{$ninstname};
    }else{
	return 1;
    }


}



#-----------------------------------------------------------------------------------------------
# crawl the DOUT_S_ROOT.locked dir and create the associated DOUT_S_ROOT hard links
#-----------------------------------------------------------------------------------------------
sub processHardlinks
{
##
# traverse the ARCHIVE_DIR_LOCKED dir and create the associated 
# hardlinks in ARCHIVE_DIR_LINKED
##
    $config{'ARCHIVE_DIR_LINKED'} = $config{'DOUT_S_ROOT'};
    mkpath( $config{'ARCHIVE_DIR_LINKED'}, {verbose => 1, error => \my $err_list} )  unless ( -d $config{'ARCHIVE_DIR_LINKED'} );
    chdir( $config{'ARCHIVE_DIR_LINKED'} ) or $logger->logdie("st_archive: Error - cannot access directory $config{'ARCHIVE_DIR_LINKED'}. Not able to save a working copy of output files on disk with error $err_list. Exiting...");
    my $readme = qq($config{'ARCHIVE_DIR_LINKED'}/../README);
    if( !-e $readme ) {
	open ( my $fh, ">", $readme) or $logger->logdie("st_archive: Error - cannont create $readme. Exiting...");
	print $fh <<"EOD";

** IMPORTANT NOTICE ** 
This directory is available for *READ ONLY* operations on CESM model 
output files that have been written by the short term archiver (st_archive). 
This directory is automatically created and populated with hard linked 
filenames when the environment variable DOUT_S_SAVE_ALL_ON_DISK is set to TRUE. 
Users may run diagnostics and post-processing routines in this location while
the model is running and st_archive is also running.

*BE AWARE* that the file names in this directory may not change but
the content of the files themselves may change while the 
st_archive is active. This is because hard links are used to point to
a specific location on disk using the same filenames as those in the 
DOUT_S_ROOT.locked directory.

EOD
;
	close($fh);
    }

#
# recursively traverse the ARHIVE_DIR_LOCKED dir and create the associated links in the ARCHIVE_DIR_LINKED dir
#
    chdir( $config{'ARCHIVE_DIR_LOCKED'} );
    my $path = '.';
    traverse( $path );
}

#-----------------------------------------------------------------------------------------------
# undoArchive routine loops through the ARCHIVE_DIR_LOCKED getting the list of archived files
# and unlinks the corresponding file in the ARCHIVE_DIR_LINKED and then moves the file back
# into the RUNDIR
#-----------------------------------------------------------------------------------------------
sub undoArchive
{
    my @archivefiles;
    find( sub {
	return unless -f;
	push @archivefiles, $File::Find::name;
	  }, 
	  $config{'ARCHIVE_DIR_LOCKED'});
    
    my $numArchivefiles = @archivefiles;
    if( $numArchivefiles > 0 ) {
	foreach my $file (@archivefiles) {
	    move( $file, $config{'RUNDIR'} );
	}
    }
}

#-----------------------------------------------------------------------------------------------
# moveFiles routine to move files into the DOUT_S_ROOT and check whether or not to preserve a 
# working copy in the rundir
#-----------------------------------------------------------------------------------------------
sub moveFiles
{
    my $keep_last_in_rundir = shift;
    my $suffix = shift;
    my $dest = shift;
    my $restdir = shift;
    my $dname = shift;
    my $runfilesref = shift;

    my @runfiles = @{$runfilesref};

    my %time = '';
    my $restFile = '';
    $logger->debug("In moveFiles..."); 
#
# make sure operating from within the RUNDIR
#
    chdir( $config{'RUNDIR'} );
#
# filter the file list in the rundir to match the suffix value from the xml
#
    my @files = grep(/${suffix}/, @runfiles);
#
# get the name of the most recent file in the $source list 
# using the last modification time
#
    my $numfiles = @files;
    if( $numfiles > 0 ) 
    {
	foreach my $file (@files) {
	    if ( -f $file ) {
		my $sb = stat($file);
		$time{$file} = $sb->mtime();
#
# splice the filename out of the runfiles array so it's not revisited
# should only match on one filename since they must be unique filenames in the rundir
#
		my @index = grep { $runfiles[$_] eq $file } 0..$#runfiles;
		splice @runfiles, $index[0], 1;
	    }
	}
#
# get the most recent copy to keep in the RUNDIR
#
	my @skeys = sort { $time{$b} <=> $time{$a} } keys %time;
	my $keepFile = $skeys[0];

	if( $keep_last_in_rundir eq 'true' ) {
#
# copy the last file into the destination dir and the restdir
#
	    $logger->debug("keepFile = $keepFile"); 
	    copy( $keepFile, $dest );

	    foreach my $file (@files) {
		if( $file ne $keepFile && -f $file ) {
#
# move all the remaining files except the last file into the destination dir
#
		    move( $file, $dest );
		}
	    }
	}
	else {
	    foreach my $file (@files) {
		move( $file, $dest );
	    }
	}
    }
# 
# also get the file that matches the dname for this suffix and
# add a hardlink to the restdir
#
    my @restFiles = grep(/${dname}/, @files);
    if( $#restFiles == 0 ) {
	my @path = split /\//, $restFiles[$#restFiles]; 
	if ( $#path >= 0 ) {
	    $restFile = $path[$#path];
	    if ( -f $dest . "/" . $restFile ) {
		link ( $dest . "/" . $restFile, $restdir . "/" . $restFile );
	    }
	}
    }
    elsif( $#restFiles > 0 ) {
	$logger->info("st_archive: multiple restart files found for suffix=$suffix");
    }
    return $numfiles, \@runfiles;
}

#-----------------------------------------------------------------------------------------------
# short term archive routine
#-----------------------------------------------------------------------------------------------
sub archiveProcess
{
    my ($XMLin) = shift;
    my $dname = shift;
    my $runfilesref = shift;

    my @runfiles = @{$runfilesref};

    $logger->debug("In archiveProcess..."); 
    my %fExt = ();
    my %comp = ();
    my ($source, $destdir, $linkdir, $outfile) = '';
#
# create the rest dir for a complete restart set with the coupler extension
#
    my $restdir = qq($config{'ARCHIVE_DIR_LOCKED'}/rest/$dname);
    mkpath( $restdir, {verbose => 1, error => \my $err_list} )  unless ( -d $restdir );
    chdir ($restdir ) or $logger->logdie("st_archive: Error - cannot access directory $restdir with error $err_list. Exiting...");
#
# copy rpointer files for all components to the $restdir
#
    chdir ($config{'RUNDIR'});
    my @rpointers = <$config{'RUNDIR'}/rpointer.*>;
    foreach my $rpointer (@rpointers) {
	    copy( $rpointer, $restdir ) or $logger->logdie("st_archive: Error - cannot copy $rpointer file to directory $restdir. Exiting...");
    }
#
# main loop through the comp_archive_spec XML elements handling the associated files accordingly
#
    my @xmlcomps = ($XMLin->findnodes('//comp_archive_spec'));
    foreach my $xmlcomp (@xmlcomps) {
	my $str = $xmlcomp->getAttribute('name');
	$str =~ s/^\s+//;
	$str =~ s/\s+$//;
	my $compname = $str;
	my $rootdir = $xmlcomp->findvalue('./rootdir');
#	my $multi = $xmlcomp->findvalue('./multi_instance');
#
# get the number of instances running for each component that supports multiple instances
#
	my $multi = "false";
	my $ninst = getcompninst($compname);
	if($ninst > 1){
	    $multi = "true";
	}
#
# loop through the file extensions for this component to copy over files with those matching extensions to the ARCHIVE_DIR locations
#
	my @infiles = ($xmlcomp->findnodes('./files/file_extension'));
	foreach my $infile (@infiles) 
	{
	    my $suffix = $infile->getAttribute('suffix');
	    my $subdir = $infile->findvalue('./subdir');
	    my $keep_last_in_rundir = $infile->findvalue('./keep_last_in_rundir');
#
# setup the subdir for this component
#
	    $destdir = qq($config{'ARCHIVE_DIR_LOCKED'}/$rootdir/$subdir);
	    mkpath( $destdir, {verbose => 1, error => \my $err_list} )  unless ( -d $destdir );
	    chdir( $destdir ) or $logger->logdie("st_archive: Error - cannot access directory $destdir with error $err_list. Exiting...");
#
# main loop to move files
# 
	    for( my $i = 1; $i <= $ninst; $i++ ) {
		my $ninst_suffix = '';
		if( $ninst > 1 ) {
		    $ninst_suffix = sprintf( '%04d', $i );
		    $ninst_suffix = qq(_$ninst_suffix);
		}
#
# build up the new suffix based on whether the XML suffix starts with a "."
#
		my $newSuffix = qq($suffix);
		if( substr($suffix, 0, 1) eq '.' ) {
		    if( $subdir eq 'logs' ) {
#
# component logs are handled a little differently
#
			$newSuffix = qq($rootdir$ninst_suffix$suffix);
		    }
		    else {
			$newSuffix = qq($config{'CASE'}.$compname$ninst_suffix$suffix);
		    }
		}
#
# move the Files to the ARCHIVE_DIR_LOCKED directory
#
		my ($numfiles, $runfilesref) = moveFiles( lc($keep_last_in_rundir), $newSuffix, $destdir, $restdir, $dname, \@runfiles );
		@runfiles = @{$runfilesref};
	    }
	}
    }
}

#-----------------------------------------------------------------------------------------------
# get the array of restart directories in a sorted list
#-----------------------------------------------------------------------------------------------
sub getRestartDirs 
{
    my %time;
    my @subdirs;
    my @sorteddirs;

    $logger->debug("In getRestartDirs..."); 

    opendir my $dh, "$config{'ARCHIVE_DIR_LOCKED'}/rest" or $logger->logdie("st_archive: Error - in getRestartDirs cannot open directory $config{'ARCHIVE_DIR_LOCKED'}/rest. Exiting...");;
    while ( my $sub = readdir $dh ) {
	next if (( $sub eq '.' ) or ( $sub eq '..' ));
	if( -d "$config{'ARCHIVE_DIR_LOCKED'}/rest/$sub" ) 
	{
	    push( @subdirs, "$config{'ARCHIVE_DIR_LOCKED'}/rest/$sub" );
	}
    }
    
    closedir $dh;

    my $numsubdirs = @subdirs;
    if( $numsubdirs > 0 )
    {
#
# get the list of restart dirs into a sorted array	
#
	foreach my $subdir (@subdirs) {
	    my $sb = stat($subdir);
	    $time{$subdir} = $sb->mtime();
	}
	@sorteddirs = reverse( sort { $time{$b} <=> $time{$a} } keys %time );
    }
    return ( @sorteddirs );
}

#-----------------------------------------------------------------------------------------------
# short term archive clean - checks the DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET option and deletes all 
# restart sets that are not either the Nth set or the last set.
#-----------------------------------------------------------------------------------------------
sub cleanArchive
{
    my ($i, $j) = 0;
    my @indeces;
    my $linkeddir;

    $logger->debug("In cleanArchive..."); 

    my @sorteddirs = getRestartDirs();
    my $numsubdirs = @sorteddirs;

# this code may no longer be needed with the new DART loop in the run script...
    if( $config{'DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET'} > 1 ) 
    {
# save every nth restart file set
	for( $i = 0; $i < $numsubdirs; $i += $config{'DOUT_S_SAVE_EVERY_NTH_RESTART_FILE_SET'} )
	{
	    push( @indeces, $i );
	}
	
	for my $index (0 .. $#indeces-1) 
	{
	    my $start = $index;
	    my $end = $indeces[$index+1];
	    for( $j = $start+1; $j < $end; $j++ ) 
	    {
# remove the restart directory - this deletes the rpointer files but not the component intermediate restart files
		rmtree $sorteddirs[$j];
# do the same for the archive dir
		if( $config{'DOUT_S_SAVE_ALL_ON_DISK'} eq 'TRUE' ) {
		    $linkeddir = $sorteddirs[$j];
		    $linkeddir =~ s/\.locked//;
                    rmtree $linkeddir;
		}
	    }
	}
    }
}

#-----------------------------------------------------------------------------------------------
# Main program
#-----------------------------------------------------------------------------------------------
sub main
{
    my $dname;
    getOptions();

    if( defined $opts{'help'} )
    {
	usage();
	exit 0;
    }

    my $runComplete = checkRun( "$config{'CASEROOT'}/CaseStatus" );
    my $XMLin  = readXMLin( "$config{'CASEROOT'}/env_archive.xml" );

    if( defined $opts{'input'} )
    {
# list the input env_archive.xml values
	listXMLin( $XMLin );
    }
    elsif( defined $opts{'output'} && $runComplete )
    {
# list the directories in the $config{'ARCHIVE_DIR_LOCKED'} directory
	$dname  = getDname();
	$logger->info("Short-term archive listing of $config{'ARCHIVE_DIR_LOCKED'}:");
	listArchive( $config{'ARCHIVE_DIR_LOCKED'} );
    }
    elsif( defined $opts{'clean'} && $runComplete )
    {
#
# clean up the archive directory and check if need to delete
# the nth occurance of restart sets
#
	$dname  = getDname();
	cleanArchive();
    }
    elsif( defined $opts{'link'} && $runComplete )
    {
#
# create the hard links in the ARCHIVE_DIR_LINKED directory
#
	$dname  = getDname();
	processHardlinks();
    }
    elsif ( defined $opts{'undo'} ) {
#
# unlink the archive process and move all files in the archive back to rundir
#
	undoArchive();
    }
    elsif ( $runComplete )
    {
# load the runfiles global array with all the files in the RUNDIR 
	my @runfiles;
        find( sub {
	    return unless -f;
	    push @runfiles, $File::Find::name;
	     }, 
	     $config{'RUNDIR'});

# get the date name from the most current coupler log file
	$dname  = getDname();

# run the short term archive process
	archiveProcess( $XMLin, $dname, \@runfiles );
#
# clean up the archive directory and check if need to delete
# the nth occurance of restart sets
#
	cleanArchive();

	if( $config{'DOUT_S_SAVE_ALL_ON_DISK'} eq 'TRUE' ) {
#
# create the hard links in the ARCHIVE_DIR_LINKED directory
#
	    processHardlinks();
	}
	$logger->info("st_archive process complete.");
	exit 0;
    }
    else
    {
	$logger->info("st_archive: run is not yet complete or was not successful. Unable to perform option...");
	exit 1;
    }
}

main(@ARGV) unless caller;


