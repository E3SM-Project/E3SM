module test_erf_r8

use pfunit_mod

use shr_kind_mod, only: &
     r8 => shr_kind_r8

use shr_spfn_mod, only: &
     erf => shr_spfn_erf, &
     erfc => shr_spfn_erfc, &
     erfc_scaled => shr_spfn_erfc_scaled

implicit none
save

! Approximately what (negative) number makes erfc_scaled overflow?
real(r8), parameter :: erfc_scaled_overflow = 26._r8

@TestParameter
type, extends(AbstractTestParameter) :: ErfR8Params
   real(r8) :: test_point
   real(r8) :: erf_val
   real(r8) :: tol = 0._r8
 contains
   procedure :: toString
end type ErfR8Params

@TestCase(testParameters={getParameters()}, constructor=new_TestErfR8)
type, extends(ParameterizedTestCase) :: TestErfR8
   real(r8) :: test_point
   real(r8) :: erf_val
   real(r8) :: tol
end type TestErfR8

contains

function new_TestErfR8(params) result(test)
  type(ErfR8Params), intent(in) :: params
  type(TestErfR8) :: test

  test%test_point = params%test_point
  test%erf_val = params%erf_val
  test%tol = params%tol
  
end function new_TestErfR8

function getParameters() result(params)
  type(ErfR8Params), allocatable :: params(:)

  params = [ &
       ErfR8Params(0._r8, 0._r8), &
       ErfR8Params(30._r8, 1._r8), &
       ErfR8Params(-30._r8, -1._r8), &
       ErfR8Params(1._r8, 0.842700792949714869341, tol=1.e-6_r8), &
       ErfR8Params(-1._r8, -0.842700792949714869341, tol=1.e-6_r8) ]

end function getParameters

function toString(this) result(string)
  class(ErfR8Params), intent(in) :: this
  character(:), allocatable :: string

  character(len=80) :: buffer

  write(buffer, '(A,F8.4,A,F8.4,A)') &
       "(point = ",this%test_point,", erf = ",this%erf_val,")"

  string = trim(buffer)

end function toString

! Check that the erf function gets the expected result.
@Test
subroutine erf_r8_has_correct_value(this)
  class(TestErfR8), intent(inout) :: this
  @assertEqual(this%erf_val, erf(this%test_point), tolerance=this%tol)
end subroutine erf_r8_has_correct_value

! Check that two runs of the erf function get identical results.
@Test
subroutine erf_r8_is_reproducible(this)
  class(TestErfR8), intent(inout) :: this
  @assertEqual(erf(this%test_point), erf(this%test_point))
end subroutine erf_r8_is_reproducible

! Check that erfc(x) = 1 - erf(x).
@Test
subroutine erfc_r8_has_correct_value(this)
  class(TestErfR8), intent(inout) :: this
  @assertEqual(1._r8 - this%erf_val, erfc(this%test_point), tolerance=this%tol)
end subroutine erfc_r8_has_correct_value

! Check that two runs of the erfc function get identical results.
@Test
subroutine erfc_r8_is_reproducible(this)
  class(TestErfR8), intent(inout) :: this
  @assertEqual(erfc(this%test_point), erfc(this%test_point))
end subroutine erfc_r8_is_reproducible

! Check that erfc_scaled(x) = exp(x**2) * (1 - erf(x)).
@Test
subroutine erfc_scaled_r8_has_correct_value(this)
  class(TestErfR8), intent(inout) :: this
  real(r8) :: erfc_scaled_expected

  ! Distinguish between where the test point has a modest value, or is too
  ! big to use a naive calculation.
  if (abs(this%test_point) < erfc_scaled_overflow) then
     erfc_scaled_expected = exp(this%test_point**2)*(1._r8 - this%erf_val)
  else
     ! For larger positive values, we could use an approximation, but this
     ! is not trivial. Large negative values should overflow; the only
     ! thing we could possibly check in that case would be to ensure that
     ! the implementation throws a floating-point error.

     ! For now, just automatically pass the test for large values.
     return
  end if

  @assertEqual(erfc_scaled_expected, erfc_scaled(this%test_point), tolerance=this%tol)
end subroutine erfc_scaled_r8_has_correct_value

! Check that two runs of the erfc_scaled function get identical results.
@Test
subroutine erfc_scaled_r8_is_reproducible(this)
  class(TestErfR8), intent(inout) :: this
  ! Skip this if we overflow.
  if (this%test_point < -erfc_scaled_overflow) return
  @assertEqual(erfc_scaled(this%test_point), erfc_scaled(this%test_point))
end subroutine erfc_scaled_r8_is_reproducible

end module test_erf_r8
