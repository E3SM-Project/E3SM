#! /bin/csh -f

# -------------------------------------------------------------------------
# Stampede build specific settings
# -------------------------------------------------------------------------

set CESM_REPO = `./xmlquery CCSM_REPOTAG -value`
if($status == 0) then
  set COMPILER = `./xmlquery COMPILER -value`
  set MPILIB   = `./xmlquery MPILIB   -value`
  set PIO_TYPENAME = `./xmlquery PIO_TYPENAME -value`
else
  echo $0 using settings from environment:
endif
echo "COMPILER=$COMPILER"
echo "MPILIB=$MPILIB"

source /etc/profile.d/tacc_modules.csh

module purge
module load TACC TACC-paths Linux cluster cluster-paths perl cmake

echo "**These are the modules loaded before compiler and mpi are selected**"
module list

if ( $COMPILER == "intel") then
  echo "Buidling for Xeon Host"
  module load intel/15.0.2
  if ( $MPILIB == "mvapich2") then
    module load mvapich2/2.1
  else if ( $MPILIB == "impi") then
     module unload mvapich2
     module load impi/5.0.2
  endif
endif
if( $MPILIB == "mpi-serial" ) then
  module load hdf5/1.8.14
  module load netcdf/4.3.3.1
else
  module load pnetcdf/1.6.0
  module load phdf5/1.8.14
  module load parallel-netcdf/4.3.3.1
endif
if ( $MPILIB == "impi") then
  setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:${TACC_HDF5_LIB}
endif
#transfer netcdf from stampdede var to cesm var
setenv NETCDF_PATH $TACC_NETCDF_DIR
setenv PNETCDF_PATH $TACC_PNETCDF_DIR
module list

# -------------------------------------------------------------------------
# Build and runtime environment variables - edit before the initial build 
# -------------------------------------------------------------------------
limit stacksize unlimited
limit datasize  unlimited

if ( $?PERL ) then
  printenv
endif
