#! /bin/csh -f

#-------------------------------------------------------------------------------
# Modules and library paths
#-------------------------------------------------------------------------------

set CESM_REPO = `./xmlquery CCSM_REPOTAG -value`
if($status == 0) then
  set COMPILER = `./xmlquery COMPILER -value`
  set MPILIB   = `./xmlquery MPILIB   -value`
  set DEBUG   = `./xmlquery DEBUG   -value`
else
  echo $0 using settings from environment:
endif
echo "COMPILER=$COMPILER"
echo "MPILIB=$MPILIB"
echo "DEBUG=$DEBUG"

if ! $?ACME_ENV_SETUP then
    setenv ACME_ENV_SETUP True
    source /opt/modules/default/init/csh
    module purge
    module load torque/5.0.1
    module load python_base/2.7.9

    if ( $COMPILER == "intel" ) then
        module load PrgEnv-intel
        module rm cray-libsci
        setenv MKL "-mkl=cluster"
    endif    
    if ( $COMPILER == "cray" ) then
        module load PrgEnv-cray
        module switch cce      cce/8.1.9
    endif    
    if ( $COMPILER == "gnu" ) then
        module load PrgEnv-gnu
        module switch gcc       gcc/4.8.0
    endif    

    module load papi/5.3.2
    module swap craype craype/2.1.1
    module load craype-ivybridge
    if( $COMPILER != "intel" ) then
        module load cray-libsci/12.2.0
    endif
    module load cray-mpich/7.2.1
    module load pmi/5.0.3-1.0000.9981.128.2.ari

    if ( $MPILIB == "mpi-serial") then
        module load cray-hdf5/1.8.11
        module load cray-netcdf/4.3.0
    else
        module load cray-netcdf-hdf5parallel/4.3.0
        module load cray-hdf5-parallel/1.8.11
        module load cray-parallel-netcdf/1.3.1.1
    endif
 
    module load perl/5.20.0
    module load cmake/2.8.11.2
    module list >& software_environment.txt

    #-------------------------------------------------------------------------------
    # Runtime environment variables
    #-------------------------------------------------------------------------------

    limit coredumpsize unlimited
    limit stacksize unlimited

    # Capture logical to physics PE assignment and active environment variable 
    # settings
    setenv MPICH_ENV_DISPLAY 1
    setenv MPICH_VERSION_DISPLAY 1
    setenv MPICH_CPUMASK_DISPLAY 1
    setenv PERL5LIB /global/project/projectdirs/ccsm1/perl5lib/lib/perl5/5.10.0

    # The environment variable below increase the stack size, which is necessary for
    # CICE to run threaded on this machine.  
    setenv OMP_STACKSIZE 64M
    if ( $?PERL ) then
      printenv
    endif

    # Capture logical to physics PE assignment and active environment variable 
    # settings
    setenv MPICH_ENV_DISPLAY 1
    setenv MPICH_VERSION_DISPLAY 1
    setenv MPICH_CPUMASK_DISPLAY 1
    # setenv MPICH_RANK_REORDER_DISPLAY 1

endif
