#! /bin/csh -f

#-------------------------------------------------------------------------------
# Modules and library paths
#-------------------------------------------------------------------------------

set CESM_REPO = `./xmlquery CCSM_REPOTAG -value`
if($status == 0) then
  set COMPILER = `./xmlquery COMPILER -value`
  set MPILIB   = `./xmlquery MPILIB   -value`
  set DEBUG   = `./xmlquery DEBUG   -value`
else
  echo $0 using settings from environment:
endif
echo "COMPILER=$COMPILER"
echo "MPILIB=$MPILIB"
echo "DEBUG=$DEBUG"

if (-e /opt/modules/default/init/csh) then
  source /opt/modules/default/init/csh
  module rm PrgEnv-intel
  module rm PrgEnv-cray 
  module rm PrgEnv-gnu
  module rm intel
  module rm cce
  module rm cray-parallel-netcdf
  module rm cray-parallel-hdf5 
  module rm pmi
  module rm cray-libsci
  module rm cray-mpich2
  module rm cray-mpich
  module rm cray-netcdf
  module rm cray-hdf5
  module rm cray-netcdf-hdf5parallel
  module rm craype-sandybridge
  module rm craype-ivybridge
  module rm craype
endif

if ( $COMPILER == "intel" ) then
    module load PrgEnv-intel 
    module load intel
    #module load mkl/13.0.3
    module use /global/project/projectdirs/ccsm1/modulefiles/edison
    if( $DEBUG == "TRUE" ) then
         module load esmf/6.2.0-defio-mpi-g
    else
         module load esmf/6.2.0-defio-mpi-O
    endif  
endif    
if ( $COMPILER == "cray" ) then
    module load PrgEnv-cray
    module switch cce      cce/8.3.7
endif    
if ( $COMPILER == "gnu" ) then
    module load PrgEnv-gnu
    module switch gcc       gcc/4.8.0
endif    

module load papi
module load craype
module load craype-haswell
if( $COMPILER != "intel" ) then
  module load cray-libsci/13.0.1
endif
module load cray-mpich
module load pmi

if ( $MPILIB == "mpi-serial") then
  module load cray-hdf5/1.8.14
  module load cray-netcdf/4.3.3.1
else
  module load cray-netcdf-hdf5parallel
  module load cray-hdf5-parallel
  module load cray-parallel-netcdf
endif

#module load perl/5.20.0
module load cmake
module load cray-petsc
module load cray-libsci

module list >& software_environment.txt

#-------------------------------------------------------------------------------
# Runtime environment variables
#-------------------------------------------------------------------------------

limit coredumpsize unlimited
limit stacksize unlimited

# Capture logical to physics PE assignment and active environment variable 
# settings
setenv MPICH_ENV_DISPLAY 1
setenv MPICH_VERSION_DISPLAY 1
setenv MPICH_CPUMASK_DISPLAY 1
#setenv PERL5LIB $PERL_DIR/lib/5.20.0

# The environment variable below increase the stack size, which is necessary for
# CICE to run threaded on this machine.  
setenv OMP_STACKSIZE 64M
if ( $?PERL ) then
  printenv
endif
