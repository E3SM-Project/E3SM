#!/bin/csh -f
# cesm_postrun with performance data archiving: 
#  ./cesm_postrun.perf_archive with the following argument keyword/value pairs, in any order
#   -machine <system name>
#   -case <case name>
#   -jobid <should be same as $PBS_JOBID for PBS and $COBALT_JOBID> for Cobalt>
#   -cesmid <$LID in cesm-lingo>
#   -user <should be same as $LOGNAME>
#   -caseroot <directory from which job script was submitted>
#   -rundir <run directory>
#   -save <TRUE|true|anything else>
#   -archive <performance archive root directory>
#   -sampling <sampling interval in seconds>

set machine = 'unknown'
set case = 'unknown'
set app_jobid = 'unknown'
set lid = 'unknown'
set user = 'unknown'
set caseroot = '.'
set run_dir = '.'
set save_timing = 'FALSE'
set save_timing_dir = '/UNSET'

set i = 1
while ($i < $#argv)
  if      ("X$argv[$i]" == 'X-machine') then
    @ i = $i + 1
    set machine = $argv[$i]
  else if ("X$argv[$i]" == 'X-case') then
    @ i = $i + 1
    set case = $argv[$i]
  else if ("X$argv[$i]" == 'X-jobid') then
    @ i = $i + 1
    set app_jobid = $argv[$i]
  else if ("X$argv[$i]" == 'X-cesmid') then
    @ i = $i + 1
    set lid = $argv[$i]
  else if ("X$argv[$i]" == 'X-user') then
    @ i = $i + 1
    set user = $argv[$i]
  else if ("X$argv[$i]" == 'X-caseroot') then
    @ i = $i + 1
    set caseroot = $argv[$i]
  else if ("X$argv[$i]" == 'X-rundir') then
    @ i = $i + 1
    set run_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-save') then
    @ i = $i + 1
    set save_timing = $argv[$i]
  else if ("X$argv[$i]" == 'X-archive') then
    @ i = $i + 1
    set save_timing_dir = $argv[$i]
  endif
  @ i = $i + 1
end

set sdate = `date +"%Y-%m-%d %H:%M:%S"`
echo "model run ended $sdate" >>& $caseroot/CaseStatus

if ($app_jobid == 'unknown') then
  if ($machine == 'titan') then

    set app_jobid = $PBS_JOBID

  else if (($machine == 'edison') || ($machine == 'cori-haswell') || ($machine == 'cori-knl')) then

    set app_jobid = $SLURM_JOB_ID

  else if ($machine == 'mira') then

    set app_jobid = $COBALT_JOBID

  endif
endif

if ($app_jobid != 'unknown') then

  set syslog_jobid = 0
  if (-e $run_dir/syslog_jobid.$app_jobid) then
    set syslog_jobid = `cat $run_dir/syslog_jobid.$app_jobid`
    /bin/rm -f $run_dir/syslog_jobid.$app_jobid
  endif

  if ($syslog_jobid != 0) then
    if { kill -0 $syslog_jobid } then 
      kill $syslog_jobid
    endif
  endif

endif

if (($save_timing == 'TRUE') || ($save_timing == 'true')) then

  if (-d $save_timing_dir) then
    cd $save_timing_dir

    if !(-d performance_archive) then
      mkdir performance_archive
      chmod 777 performance_archive
      chmod +s performance_archive
    endif
    cd performance_archive

    if !(-d $user) then
      mkdir $user
      chmod 775 $user
      chmod +s $user
    endif
    cd $user

    if !(-d $case) then
      mkdir $case
      chmod 775 $case
      chmod +s $case
    endif
    cd $case

    if !(-d $lid) then
      mkdir $lid
      chmod 775 $lid
      chmod +s $lid
    endif

    cd $caseroot

    if ($machine == 'titan') then

      set string = `ls $app_jobid*OU`
      cp -p $string $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
      chmod a+r $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid

    else if ($machine == 'mira') then

      set string = `ls $app_jobid*output`
      cp -p $string $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
      chmod a+r $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid

      set string = `ls $app_jobid*cobaltlog`
      cp -p $string $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
      chmod a+r $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid

    else if (($machine == 'edison') || ($machine == 'cori-haswell') || ($machine == 'cori-knl')) then

      set string = `ls $case`
      cp -p $string $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
      chmod a+r $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid

    endif

  endif

endif
